10 '*** COLUMNAR.ENC *** COLUMNAR ENCIPHERING PROGRAM BY M.Barlow Sep 1986 based on MYS/ENC of 1984 aug 87 ibm version aug 1 88
30 '
40 KEY OFF:CLS:CLEAR :DEFINT A-Z              'housekeeping
45 AL$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
50 GOSUB 620                                  'load example
60 '
                    ---- start here ----
70 CLS:PRINT DATE$ TAB(28)"* COLUMNAR ENCIPHERMENT *":PRINT TAB(30)STRING$(21,"-")
80 PRINT:PRINT TAB(34)"By Mike Barlow":GOTO 95
85 LOCATE 12,1:PRINT"Is this to be a Complete or Incomplete Columnar? (C*/I): ";: LINE INPUT TYPE$
87 ON INSTR("CcIi",TYPE$)GOTO 88,88,89,89:GOTO 70
88 CLS:PRINT DATE$ TAB(25)"* COMPLETE COLUMNAR ENCIPHERMENT *":PRINT TAB(27)STRING$(30,"-"):GOTO 90
89 CLS:PRINT DATE$ TAB(24)"* INCOMPLETE COLUMNAR ENCIPHERMENT *":PRINT TAB(26)STRING$(32,"-"):TY=1
90 PRINT:PRINT TAB(34)"By Mike Barlow":PRINT:COLOR 0,7:PRINT "Plaintext is:":COLOR 7,0:PRINT PT$:GOTO 130
95 PT$="":LOCATE 7,1:PRINT "Enter the plaintext (UPPER CASE, no punctuation, max 240 chars): ";:LINE INPUT PT$:PRINT
100 IF PT$=""THEN PT$=XP$:PRINT PT$
110 GOSUB 510                               'delete blanks in plaintext
120 LP=LEN(P$):CC$="   ("+RIGHT$(STR$(LP),LEN(STR$(LP))-1)+" letters).":PRINT CC$:PT$=PT$+CC$                               'add char count
125 GOTO 85
130 '
135 IF TY=1 THEN LL=LP/18 ELSE LL=LP/15  'ACA&U limits
140 KW$="":LOCATE 12,1:PRINT SPC(79):LOCATE 12,1:PRINT"Enter the keyword ("LL"- 9 chars, UPPER CASE): ";:X=POS(0):Y=CSRLIN:LINE INPUT KW$
150 IF KW$=""THEN KW$=XK$:LOCATE Y,X:PRINT KW$
160 LK=LEN(KW$):IF LK>9 THEN PRINT"KEYWORD TOO LONG. TRY AGAIN.":GOTO 140
170 IF ASC(MID$(KW$,2,1))>96 THEN PRINT"UPPER CASE PLEASE. TRY AGAIN.":GOTO 140
180 GOSUB 430                     'get pattern of KW$
190 NB=-INT(-LP/LK)               'NB is # of blocks of text
195 IF TY=0 AND NB<>LP/LK THEN PRINT:PRINT"ERROR: text is not correct length for a Complete Columnar. Try a new text or newkeyword length.":LOCATE 23,29:PRINT"Press <ENTER> to proceed ";:LINE INPUT Q$:LOCATE 23,29:PRINT SPC(50):GOTO 10
196 IF TY=1 AND NB=LP/LK THEN PRINT:PRINT"ERROR: text is not correct length for an Incomplete Columnar. Try a new text or new keyword length.":LOCATE 23,29:PRINT"Press <ENTER> to proceed ";:LINE INPUT Q$:LOCATE 23,29:PRINT SPC(50):GOTO 10
200 DIM P$(NB),OP$(NB)
205 '
216 PRINT :PRINT"Column order = "PA$"
220 FOR N=1 TO NB:P$(N)=MID$(P$,1+(N-1)*LK,LK): NEXT
250 PRINT:PRINT:COLOR 0,7:PRINT"Cipher text =  ";:COLOR 7,0:PRINT" ";
260 '
    ---- get posn numbers for same pattern # and repeat for each block ----
270 M=1:N=1:K=0        ' M is block #, N is pattern #, K is # of same pattern #s
280 K=K+1: X=INSTR(PA$,RIGHT$(STR$(N),1)): IF X=0 THEN 290ELSE MID$(PA$,X,1)="*":X(K)=X:GOTO 280
290 K=K-1:IF K=0 THEN IF Q$="N"THEN PRINT OP$:GOTO 360 ELSE 350
300 OP$(M)="": FOR J=1 TO K: OP$(M)=OP$(M)+MID$(P$(M),X(J),1): NEXT
310 OP$=OP$+OP$(M)
320 M=M+1: IF M=NB+1 THEN 330 ELSE 300
330 N=N+1:M=1:K=0:GOTO 280
340 '
                   ---- print in fives ----
350 Z$="":FOR M=1 TO LP STEP 5
360   X$=  MID$(OP$,M,5)+"   "
370   PRINT X$;
380   Z$=Z$+X$
390 NEXT
400 OP$=Z$:GOTO 9250
420 'goto420
421 RUN"menu.bas
425 '
    --- conv KW$ into order table PA$ ---
430 FOR N=1 TO LEN(KW$)
435  X$=MID$(KW$,N,1)                            'get one char
440  Y=INSTR(AL$,X$):IF Y=0 THEN 450             'where is it in alphabet?
445  MID$(AL$,Y,1)=CHR$(ASC(MID$(AL$,Y,1))+32)   'put char in l.c
450 NEXT:N$=""
455 FOR N=1 TO 26
460  X$=MID$(AL$,N,1)                            'get modified alpha chars
465  IF ASC(X$)<91 THEN 475                      'if u.c then skip
470  N$=N$+CHR$(ASC(X$)-32)                      'else change back to u.c
475 NEXT:PA$=""                                  'now you've got the input char                                                   in alpha order
480 FOR N=1 TO LEN(N$):Z=1
485   X=INSTR(Z,KW$,MID$(N$,N,1)):IF X=0 THEN 495 ELSE Z=X+1 'so compare the two                                                              sets
490   PA$=PA$+RIGHT$(STR$(X),1):GOTO 485
495 NEXT:RETURN
500 '
           ---- eliminate blanks in plaintext ----
510 P$=""
520 FOR N=1 TO LEN(PT$) 
530  X$=MID$(PT$,N,1)
540  IF X$=" " THEN 560
550  P$=P$+X$
560 NEXT:RETURN
620 '
                    ----  examples ----
640 XP$="HAVE YOU NOTICED THAT THE EARLY BIRD GETS THE MORNING WORM ALMOST EVERY TIME":XK$="QUINCES":XS$="M.Barlow"
650 RETURN
9250 '
9260 LOCATE 23,1:PRINT"Do you wish to <P*>rint this for the Cm, do <A>nother, or <Q>uit? ";:LINE INPUT Q$
9270 ON INSTR("PpAaQq",Q$)GOTO 9280,9280,10,10,9500,9500:GOTO 9260
9280 ON ERROR GOTO 18000
9290 WIDTH "lpt1:",255    'needed by GWBASIC
9300 EM(0)=-12971:EM(1)=23813:EM(2)=-28469
9310 X=VARPTR(EM(0))
9320 CALL X
9340 FOR N=1 TO 20:LPRINT:NEXT   'space to bottom of page
9350 IF TY=1 THEN LPRINT "INCOMPLETE COLUMNAR encipherment by Mike Barlow   "DATE$:LPRINT:GOTO 9360
9355 LPRINT "COMPLETE COLUMNAR encipherment by Mike Barlow   "DATE$:LPRINT
9360 LPRINT OP$
9370 ON ERROR GOTO 0
9380 LPRINT CHR$(12):GOTO 9260
9500 LOCATE 23,1:PRINT SPC(79):LOCATE 23,39:PRINT "end"
9510 LOCATE 23,1:END
18000 '   --- printer error trap ---
18010 IF ERR=24 OR ERR=25 OR ERR=27 THEN LOCATE 23,1:PRINT SPC(79):LOCATE 23,15: PRINT "Printer not ready; press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79):RESUME
18020 ON ERROR GOTO 0:STOP
60000 ' TEXT/KEY ENTRY REVERSED; mods to non-default data entry; Cm printout added; ACA limits added. NB cannot handle key > 9 because of double digit problems. Oct 1989
65000 SAVE"columnar.enc",A
