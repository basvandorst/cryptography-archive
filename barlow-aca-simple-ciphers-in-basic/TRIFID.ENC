5 '***** check line 280 against orig. What is this supposed to do?
10 '** TRIFID.ENC ** BY M.Barlow 1986 Jun 24A ibm version March 88
20 '
25 VIEW PRINT
30 CLEAR 1500:DEFINT A-Z:GOSUB 990:DIM X(28),C(200)
40 DIM N%,M%,K%,X$,KK%,OP$,L%,AL$,X%,P$,P%,PT$,KW$,NK$,DU$,J%,LP%,P1%,LO%,XP$,XK$,XP%,RK$,NC%,LK%,Q$,C%,X%,J%
60 DEF FN CC$(LP,PT$)=PT$+"   ("+RIGHT$(STR$(LP),LEN(STR$(LP))-1)+" chars)." 'add char count to string, neatly.
65 A1$="111111111222222222333333333":A2$="111222333111222333111222333":A3$="123123123123123123123123123"
70 '
80 CLS:PRINT TAB(1+24)"* TRIFID ENCIPHERMENT *":PRINT TAB(1+26)STRING$(19,"-")
90 PRINT TAB(1+28)"By Mike Barlow"
100 '
105 LOCATE 8,1:PRINT"Do you wish to print a work sheet as you go? (Y/N*): ";:LINE INPUT Q$:IF Q$="Y"OR Q$="y"THEN QY=1:GOSUB 985:LPRINT"Work sheet for Trifid encipherment by Mike Barlow:":LPRINT:ELSE QY=0
110 PT$="":LOCATE 10,1:PRINT CHR$(31)"Enter the plaintext (UPPER CASE, 120-150 chars): ";:PRINT:LINE INPUT PT$
120 IF PT$=""THEN PT$=XP$:PRINT CHR$(30);PT$
130 GOSUB 690                      'delete blanks in plaintext
140 LP=LEN(P$):PRINT"("LP"chars)":'PT$=FN CC$(LP,PT$)  'add char count
145 IF QY=1 THEN LPRINT"Plaintext is: "PT$:LPRINT
150 'LOCATE 11,1: PRINT PT$                 'print it again, Sam.
160 KW$="":PRINT:PRINT"Enter the keyword (UPPER CASE): ";:X=POS(0):LINE INPUT KW$
170 IF KW$=""THEN KW$=XK$:PRINT TAB(1+X)CHR$(30);KW$
180 RK$=KW$:GOSUB 750              'check for repeats in KW
190 IF NK$<>KW$THEN KW$=NK$:PRINT TAB(1+X)CHR$(30);KW$:PRINT TAB(1+X-10)"(Duplicate letters removed)"
200 LK=LEN(KW$):GOSUB 820          'set up the key alphabet
210 P=0:PRINT:PRINT"Enter the period (5-15): ";:X=POS(0):LINE INPUT X$
220 IF X$=""THEN P=XP:PRINT TAB(1+X)CHR$(30);P ELSE P=VAL(X$):IF P>15 THEN PRINT"TOO LARGE! Won't fit on the screen! Try again...":GOTO 210
230 NC=INT(LEN(P$)/P):IF NC>9 THEN PRINT"TEXT IS TOO LARGE FOR THIS PERIOD.":PRINT:PRINT"<C>ut the text or try a <D>ifferent period? (C*/D): ";:LINE INPUT Q$:IF Q$="D"OR Q$="d"THEN 210ELSE P$=LEFT$(P$,P*10)
240 '--- display on screen ---
250 CLS:FOR N = 1 TO LEN(P$)/P+1
260   FOR M = 1 TO P:X$=MID$(P$,1+(M-1)+(N-1)*P,1):IF LEN(X$)=0 THEN 300
270    X=INSTR(AL$,X$)
275    ROW=1+(M-1): COL=1+6*(N-1)
280    LOCATE ROW,COL:PRINT X$;X(X);
290   NEXT M
300 NEXT N:IF QY=1 THEN LPRINT"Keyed alphabet = "AL$:LPRINT TAB(1+17)A1$:LPRINT TAB(1+17)A2$:LPRINT TAB(1+17)A3$:SYSTEM"JKL":LPRINT"Readout of digits vertically:"
305  LOCATE 1+1+P,1:PRINT"Reading the numbers in vertical threes:";: GOSUB 980:PRINT
310 '--- read out vert columns of numbers in threes ---
330 K=1:KK=1
331 'K counts vert 3s, KK counts groups, L is tab posn of first number column, M counts columns in group,
332 'N counts vertical numbers
340 FOR L = 3 TO 60 STEP 6
350   FOR M = 1 TO 3
360     FOR N = 0 TO P-1
369      COL=L+M-1: ROW=1+N
370      J(K)=SCREEN (ROW,COL)-48:IF J(K)<0 THEN N=P-1:GOTO 410
380      IF K=3 THEN C(KK)=100*J(1)+10*J(2)+J(3):PRINT C(KK);:IF KK/12=INT(KK/12)THEN LOCATE 2+1+P,1:PRINT STRING$(79," "): LOCATE 2+1+P,1: KK=KK+1 : ELSE KK=KK+1
385     IF(QY=1 AND K=3)THEN LPRINT USING"###  ";C(KK-1);
390      K=K+1:IF K=4 THEN K=1
400     NEXT N
410   NEXT M
420 NEXT L:PRINT: PRINT"Converting back to letters, and spacing the ciphertext:";:GOTO 450
440 '--- convert back to letters ---
450 FOR M = 1 TO LEN(P$)
452   FOR N=1 TO 27
460    IF X(N)=C(M) THEN X$=MID$(AL$,N,1): OP$=OP$+X$: PRINT ".";
470   NEXT N
472 NEXT M
475 IF QY=1 THEN LPRINT" ":LPRINT"Conversion back to characters:":FOR N=1 TO LEN(P$):LPRINT" "MID$(OP$,N,1)"   ";:NEXT:LPRINT
480 '--- display answer ---
490 X$=OP$:OP$="":LO=LEN(X$)
500 CLS:PRINT TAB(1+24)"* TRIFID ENCIPHERMENT *"TAB(69)DATE$:PRINT TAB(1+26)STRING$(19,"-")
510 PRINT TAB(1+28)"By Mike Barlow"
520 PRINT TAB(1+60)DATE$
530 '--- put in a blank every P2 ---
540 P2=P:FOR N=1 TO LEN(X$)
550  OP$=OP$+MID$(X$,N,1)
560  IF N/P2=INT(N/P2)THEN OP$=OP$+" "
570 NEXT
580 LOCATE 4,1:PRINT"Plaintext is:":LOCATE 5,1:PRINT PT$
590 LOCATE 8,1:PRINT"Keyword = "RK$TAB(1+41)"Period ="P
600 LOCATE 10,1:PRINT"Ciphertext is:":LOCATE 11,1:PRINT FN CC$(LO,OP$):IF QY=1 THEN LPRINT" ":LPRINT"Spaced ciphertext (period"P") is: "FN CC$(LO,OP$)
610 BEEP:BEEP
620 LOCATE 15,1:PRINT"Choose: <P>rint this, <S>tore it, do <A>nother, or <Q>uit? ";:LINE INPUT Q$
630 ON INSTR("PpAaQqSs",Q$)GOTO 640,640,10,10,660,660,49000,49000:GOTO 620
640 LOCATE 20,1:PRINT CHR$(30)TAB(1+10)"Encipherment by Michael Barlow on the IBM";:PRINT"Please use <PrtSc> key for this: "
650 GOTO 650
660 LOCATE 20,33:PRINT"end":END
670 GOTO 670
680 '--- eliminate blanks in PT$ ---
690 P$=""
700 FOR N=1 TO LEN(PT$)
710  X$=MID$(PT$,N,1)
720  IF X$=" "THEN 740
730  P$=P$+X$
740 NEXT:RETURN
750 '--- eliminate duplicate letters in KW$ ---
760 NK$="":FOR N=1 TO LEN(KW$)
770   DU$=MID$(KW$,N,1)'        for each keyword letter
780   IF INSTR(NK$,DU$)THEN 800 ' has it been used already?
790   NK$=NK$+DU$ '             if not, add to new key
800 NEXT:RETURN
810 '--- set up number array of 3-digit groups ---
820 N=1
830 FOR J=1 TO 3
840  FOR K=1 TO 3
850    FOR L=1 TO 3
860    X(N)=100*J+10*K+L
870    N=N+1
880 NEXT:NEXT:NEXT
890 '--- make up key alphabet of 27 chars ---
900 AL$="ABCDEFGHIJKLMNOPQRSTUVWXYZ#"
910 FOR N=1 TO LEN(KW$)
920   X=INSTR(AL$,MID$(KW$,N,1))     'find posn of KW letter in alphabet
930   X$=LEFT$(AL$,X-1)              'take the alphabet to the left of it
940   AL$=X$+RIGHT$(AL$,LEN(AL$)-X)  'and add to the remainder
950 NEXT
960 AL$=KW$+AL$                      'now put the keyword in front
970 RETURN
975 ' ---  misc ---
980 FOR NN=1 TO 700:NEXT:RETURN    'delay
985 IF(PEEK(14312)OR 15)=63 THEN RETURN ELSE LOCATE 8,23:PRINT"PRINTER NOT READY.";:GOTO 985 'printer check
990 ' --- examples ---
1000 XP$="TRIFIDS ARE FRACTIONATED CIPHERS":XK$="EXTRAORDINARY":XP=10:'ACA&U p20 takes 8 secs for 29 chars
1010 XP$="I ALWAYS SAY THAT THE EARLY BIRDS GET THE MORNING WORMS EVERY TIME":XP$=XP$+XP$:XK$="MIKEBARLOWISASUPERPROGRAMMER":XP=15:' takes 24 secs for 108 chars
1011 XP$="I KNOW THAT YOU BELIEVE YOU UNDERSTAND WHAT YOU THINK I SAID BUT I AM NOT SURE YOU REALIZE THAT WHAT YOU HEARD IS NOT WHAT I MEANT WHEN I SAID IT":XK$="MASTERFUL":XP=12
1020 RETURN
65000 SAVE"TRIFID.enc",A
