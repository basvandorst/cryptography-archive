10 '** CBENC CHECKERBOARD ENCIPHERER ** NOV 87
15 ' by Mike Barlow, Montreal, Canada (Version at 9090)
16 'crib encipherment not activated at line 4000 but OK.
18 KEY OFF
20 GOTO 9000
500 '
 --- print the single keysquare ---
510 LOCATE OS,8:PRINT ST$;:LOCATE OS+1,8:PRINT"----------";
520 FOR K=0 TO 4:LOCATE OS+2+K,6:PRINT MID$(KS$,K+1,1)CHR$(179);:NEXT
530 RETURN
600 '
 --- print the double keysquare ---
610 LOCATE OS-1,8:PRINT S5$;:LOCATE OS,8:PRINT S6$;:LOCATE OS+1,8:PRINT"----------";
620 FOR K=0 TO 4:LOCATE OS+2+K,5:PRINT MID$(K3$,K+1,1)MID$(K4$,K+1,1)CHR$(179);:NEXT
630 RETURN
999 '
           --------> misc subs <--------
1000  FOR N9=1 TO 24-Y:PRINT SPC(79):NEXT:RETURN 'erase to EOP
1020 LOCATE 23,1:PRINT SPC(79):LOCATE 23,29:PRINT"Press <ENTER> to proceed ";:LINE INPUT Q$:RETURN
1050 FOR N=1 TO D9:NEXT:RETURN  'delay
2000 ' --- SUB: route encipherer ---
2010 DIM DS(30) 'dim > max (a+b)
2020 GOTO 3220
2040 ' --- horiz ---
2050 FOR Y=1 TO B
2060  FOR X=1 TO A
2070    LOCATE Y+4,ST+(X-1)*2:PRINT MID$(NW$,X+(Y-1)*A,1)" ";
2080  NEXT
2090 NEXT
2110 GOTO 3120
2120 ' --- alt horiz ---
2130 FOR Y=1 TO B
2140  FOR X=1 TO A
2150   IF Y/2=INT(Y/2)THEN 2160 ELSE 2170 'odd/even switch
2160    LOCATE Y+4,ST+(A-X)*2:GOTO 2180   'even
2170    LOCATE Y+4,ST+(X-1)*2             'odd
2180    PRINT MID$(NW$,X+(Y-1)*A,1)" ";
2190  NEXT
2200 NEXT
2210 GOTO 3120
2220 ' --- verticals ---
2230 FOR X=1 TO A
2240  FOR Y=1 TO B
2250    LOCATE Y+4,ST+(X-1)*2
2260    PRINT MID$(NW$,(X-1)*B+Y,1)" ";
2270  NEXT
2280 NEXT
2290 GOTO 3120
2300 ' --- alt verticals ---
2310 FOR X=1 TO A
2320  FOR Y=1 TO B
2330   IF X/2=INT(X/2)THEN 2340 ELSE 2350 'odd/even switch
2340    LOCATE B-Y+5,ST+(X-1)*2:GOTO 2360 'even
2350    LOCATE Y+4,ST+(X-1)*2             'odd
2360    PRINT MID$(NW$,(X-1)*B+Y,1)" ";
2370  NEXT
2380 NEXT
2390 GOTO 3120
2400 ' --- spirals ---
2410 A1=A:B1=B:PT$=RP$:STY=0:STX=ST 'stx & sty are incr of top LH corner
2420 X$=LEFT$(NW$,2*(A1-1)+2*(B1-1)):Y$=RIGHT$(NW$,A*B-(2*(A1-1)+2*(B1-1)))
2430 IF ALT=0 THEN GOTO 2530
2440 '
                ---- anticlockwise spiral ----
2450  FOR Y=1 TO B1:LOCATE Y+4+STY,STX:PRINT MID$(X$,Y,1)" ";:NEXT 'LHS top-bottom
2460   IF B>=A THEN IF A1<=2 OR B1<=2 THEN 3120
2465 IF A=B AND X=2 AND Y=2 THEN 3120 'odd sided squares
2470  FOR X=1 TO A1-1:LOCATE B1+4+STY,STX+X*2:PRINT MID$(X$,B1+X,1)" ";:NEXT  'bottom l-r
2480   IF A>=B THEN IF A1<=2 OR B1<=2 THEN 3120
2490  FOR Y=1 TO B1-1:LOCATE B1-Y+4+STY,STX+(A1-1)*2:PRINT MID$(X$,A1+B1+Y-1,1)" ";:NEXT       'RHS bottom-top
2500  FOR X=1 TO A1-2:LOCATE 5+STY,STX+(A1-X-1)*2:PRINT MID$(X$,A1+B1+B1+X-2,1)" ";:NEXT              'top r-l
2510 GOSUB 2610:GOTO 2450
2520 '
                  ---- clockwise spiral ----
2530  FOR X=1 TO A1:LOCATE 5+STY,STX+(X-1)*2:PRINT MID$(X$,X,1)" ";:NEXT 'top L-R
2540   IF A>=B THEN IF A1<=2 OR B1<=2 THEN 3120
2545 IF A=B AND X=2 AND Y=2 THEN 3120 'odd sided squares
2550  FOR Y=1 TO B1-1:LOCATE Y+5+STY,STX+2*(A1-1):PRINT MID$(X$,A1+Y,1)" ";:NEXT 'RHS top-bottom
2560   IF B>=A THEN IF A1<=2 OR B1<=2 THEN 3120
2570  FOR X=1 TO A1-1:LOCATE B1+4+STY,STX+(A1-X-1)*2:PRINT MID$(X$,A1+B1+X-1,1)" ";:NEXT 'bottom r-l
2580   IF A>B THEN IF B1<=2 OR A1<=2 THEN 3120
2590  FOR Y=1 TO B1-2:LOCATE B1-Y+4+STY,STX:PRINT MID$(X$,A1+B1+A1+Y-2,1)" ";:NEXT 'LHS bottom-top
2600 GOSUB 2610:GOTO 2530
2610 '
        --- SUB: now change to the next smaller circle ---
2620 STY=STY+1:STX=STX+2:A1=A1-2:B1=B1-2:IF A1<=0 OR B1<=0 THEN 3120
2630 'ETURN:IF A1=1 AND B1=1 THEN X$=RIGHT$(NW$,1): GOTO 2660 'pop in the last one of a square
2640 'IF LEN(Y$)<2*(A1-1)+2*(B1-1)THEN X$=RIGHT$(NW$,A1*B1):GOTO 2660
2650 X$=LEFT$(Y$,1+2*(A1-1)+2*(B1-1)):Y$=RIGHT$(Y$,A1*B1-(2*(A1-1)+2*(B1-1)))
2660 RETURN
2670 '                  --- diagonals ---
2680 X=1:DC=1:N=0:ST=ST-2:Y=0:RV$="":IF ALT=0 THEN NW$=RP$
2690 GOSUB 2920            'print 1st char
2700 DC=DC+1              'incr diagonal counter
2710 IF DC=A+B THEN GOTO 3120
2720 GOSUB 2860            'step right
2730 GOSUB 2920            'print
2740 RY=Y:RX=X            'remember start of diag
2750 GOSUB 2820            'calculate # of diag steps DS
2760 FOR J=1 TO DS
2770  GOSUB 2890           'step diagonally down to left
2780  GOSUB 2920           'print char
2790 NEXT
2800 Y=RY:X=RX            'restore top-of-diagonal values
2810 GOTO 2700
2820 '
       ---  SUB: calculate # of diag steps reqd ---
2830 IF DC<=L THEN DS=DC-1:RETURN    'first few steps
2840 IF DC>LL THEN DS=DS-1:RETURN    'end ones
2850 DS=L:RETURN                     'middle ones
2860 '
         ---  SUB: step right  ---
2870 IF X=A THEN Y=RY+1:RETURN
2880 X=X+1:RETURN
2890 '
         --- SUB: step diagonally ---
2900 X=X-1:IF Y=B THEN RETURN
2910 Y=Y+1:RETURN
2920 '
          ---  SUB: print n+1th char  ---
2930 N=N+1
2940 LOCATE Y+5,ST+X*2:PRINT MID$(NW$,N,1)" ";:RETURN
2950 '
           --- reverse alt text lines ---
2960 NW$=RP$
2970 X$="":RV$="":FOR DC=1 TO A+B-1
2980  GOSUB 2820
2990  DS(DC)=DS+1
3000 NEXT    'now we have the alt line lengths
3010 FOR C=1 TO LP
3020  RV$=RV$+MID$(NW$,LP-C+1,1)
3030 NEXT    'now we have the reversed text
3040 N=0:FOR J=1 TO DC-1
3050  N=N+DS(J) 'count total chars
3060  IF J/2 <> INT(J/2) THEN 3080 'odd diagonals
3070  X$=X$+MID$(NW$,LEN(X$)+1,DS(J)):GOTO 3090 'read fwd
3080  X$=X$+MID$(RV$,LP+1-N,DS(J)): 'read reversed txt
3090 NEXT
3100 NW$=X$:GOTO 2670
3110 '
               --- housekeeping ---
3120 LOCATE 12,1:Y=12:GOSUB 1000:LOCATE (B+7),1:PRINT "This pattern is: "SH$(SHAPE)" using alphabet key "KK$
3125 LOCATE B+11,18:PRINT"Press <ENTER> to proceed, <N> for new pattern: ";:LINE INPUT Q$:IF Q$="N" OR Q$="n"THEN 3210
3130 '
          --- read out the block h & v ---
3140 ST=RS:X$="":FOR Y=1 TO B: FOR X=1 TO A
3150   X$=X$+CHR$(SCREEN(Y+4,6+X*2))
3160 NEXT:NEXT:H$=X$
3170 X$="":FOR X=1 TO A:FOR Y=1 TO B
3180   X$=X$+CHR$(SCREEN(Y+4,6+X*2))
3190 NEXT:NEXT:V$=X$
3200 RETURN
3210 LOCATE 16,1:PRINT SPC(79):ST=RS:GOTO 3260
3220 '
               ---- initialisation ----
3230 RP$=NW$:A=5:B=5:LP=LEN(NW$):ST=8:RS=ST
3240  L=B-1: LL=A 'used in diagonals
3250 '
3260 LOCATE 16,1:PRINT"Now choose the basic shape for the Route:
3270 PRINT TAB(7)"1- Horizontal*"TAB(23)"2- Alt Horiz"TAB(39)"3- Vertical"TAB(55)"4- Alt Vert":PRINT TAB(7)"5- C/W Spiral"TAB(23)"6- AC/W Spiral"TAB(39)"7- Diagonal"TAB(55)"8- Alt Diag:";:INPUT SH$:IF SH$="" THEN SH$="1"
3275 SHAPE=VAL(SH$)
3280 IF SH$="6" OR SH$="8"THEN ALT=1 ELSE ALT=0 'set alternate diag flag
3290 ON INSTR("12345678",SH$) GOTO 2040,2120,2220,2300,2400,2400,2670,2950
3300 GOTO 3260
4000 '
   --- Caesar the crib (limited +/- 4) ---
4010 CLS:PRINT"What is the plaintext crib? (UPPER CASE):";:LINE INPUT A$
4020 CLS:FOR N=-4 TO +4
4030   LOCATE 65321!,65513!:PRINT N;
4050   FOR K = 1 TO LEN(A$)
4060     T$=MID$(A$,K,1)'   processat each letter in turn
4080     CD=ASC(T$)+N:IF CD>90 THEN CD=CD-26
4081     IF CD<65 THEN CD=CD+26
4090     NU$=CHR$(CD):PRINT NU$;
4100   NEXT
4110 NEXT:PRINT
4120 PRINT"Choose the enciphered crib (UPPER CASE): ";:LINE INPUT CB$:IF LEN(CB$)<>LEN(A$)THEN PRINT"** ERROR ** Try again.":PRINT CHR$(27);:PRINT CHR$(27);:GOTO 4120
4130 CLS:LOCATE 1,46:PRINT"Crib: "CB$;
4140 RETURN
7000 '
           ---->  HELP routine <----
7010 LOCATE 4,1:PRINT"The Checkerboard cipher is based on a 5x5 keysquare, I and J sharing one cell.":PRINT "(The program will change any J's to I's for you).":PRINT:PRINT"Two forms are in use:":PRINT " "
7012 PRINT"The Single Checkerboard uses one 5-letter keyword along the top and LHS of the"
7015 PRINT"square.  Each plaintext letter is substituted by its X and Y co-ordinates from":PRINT"the keywords. There must be no repeating letters in either keyword, but they maycontain the same letters, and J is also permitted."
7020 PRINT:PRINT"The more complex Double Checkerboard uses two 5-letter keywords each at the top and side, thus giving four alternative substitutions for each plaintext letter."
7025 PRINT"In either pair, there must be no repeats.
7030 PRINT:PRINT TAB(21)"TYPE <CODIC5.DAT> for keyword suggestions.":PRINT:PRINT TAB(10)"Press ENTER in answer to any prompt to use the built-in example."
7040 PRINT:PRINT TAB(28)"Press <ENTER> to proceed ";:LINE INPUT Q$:RETURN
8999 '
             ----> initialisation <----
9000 CLEAR :DEFINT A-Z:CLS
9040 AL$="ABCDEFGHIKLMNOPQRSTUVWXYZ" '25 letters
9060 SH$(1)="REGULAR HORIZONTALS":SH$(2)="ALTERNATE HORIZONTALS":SH$(3)="REGULAR VERTICALS":SH$(4)="ALTERNATE VERTICALS":SH$(5)="CLOCKWISE SPIRAL":SH$(6)="ANTICLOCKWISE SPIRAL":SH$(7)="REGULAR DIAGONALS":SH$(8)="ALTERNATE DIAGONALS"
9070 OS=3
9090 PRINT DATE$;TAB(29)"CHECKERBOARD ENCIPHERER"TAB(69)"Version JA7":PRINT TAB(29) STRING$(23,"-"):GOSUB 20000
9120 DT$=DATE$
9140 LOCATE 9,32:PRINT "Need Help? (Y/N*): ";:LINE INPUT Q$:IF Q$="Y"OR Q$="y"THEN NH=1:GOSUB 7000
9150 GOTO 10000
9240 '
9250 '   --- Cm format printing etc ---
9260 LOCATE 23,1:PRINT"Do you wish to <P*>rint this for the Cm, do <A>nother, or <Q>uit? ";:LINE INPUT Q$
9270 ON INSTR("PpAaQq",Q$)GOTO 9280,9280,10,10,9500,9500:GOTO 9260
9280 ON ERROR GOTO 18000
9290 WIDTH "lpt1:",255    'needed by GWBASIC
9300 EM(0)=-12971:EM(1)=23813:EM(2)=-28469
9310 X=VARPTR(EM(0))
9320 CALL X
9340 FOR N=1 TO 20:LPRINT:NEXT   'space to bottom of page
9350 LPRINT "CHECKERBOARD encipherment by Mike Barlow   "DATE$:LPRINT
9360 LPRINT OP$
9370 ON ERROR GOTO 0
9380 LPRINT CHR$(12):GOTO 9260
9500 LOCATE 23,1:PRINT SPC(79):LOCATE 23,39:PRINT "end"
9510 LOCATE 23,1:END
9520 :
10000 LOCATE 3,1: FOR N=1 TO 21:PRINT SPC(79):NEXT:LOCATE 5,1:PRINT"Is this to be a <S*>ingle or <D>ouble Checkerboard? (S*/D): ";:X=POS(0):LINE INPUT Q$:IF Q$="D"OR Q$="d"THEN GOTO 11000 ELSE CF=1:IF Q$<>"S"THEN LOCATE 5,X:PRINT"SINGLE"
10010 KS$="":PRINT:PRINT"Enter the first (side) key (UPPER CASE, 5 chars): ";:X=POS(0):LINE INPUT KS$:IF KS$=""THEN KS$=K1$:LOCATE 7,X:PRINT KS$
10020 KT$="":LOCATE 9,1:PRINT"Enter the second (top) key (UPPER CASE, 5 chars): ";:X=POS(0):LINE INPUT KT$:IF KT$=""THEN KT$=K2$:LOCATE 9,X:PRINT KT$
10025 X$="":FOR N=1 TO 5:ST$=ST$+MID$(KT$,N,1)+" ":NEXT: 'space top key
10030 LOCATE 13,1:KK$="":PRINT"Enter the alphabet key (UPPER CASE, up to 26 chars): ";:X=POS(0):LINE INPUT KK$:IF KK$=""THEN KK$=K9$:LOCATE 13,X:PRINT KK$
10032 X$=KK$+AL$:NW$=""
10034 FOR N=1 TO LEN(X$)
10036   DU$=MID$(X$,N,1)
10038   IF INSTR(NW$,DU$) THEN 10042
10040   NW$=NW$+DU$
10042 NEXT:LOCATE 15,1:PRINT"Keyed alphabet is "NW$
10045 GOSUB 1020:LOCATE 3,1:Y=3:GOSUB 1000
10060 IF CF=1 THEN GOSUB 500 ELSE GOSUB 600  'print square
10070 GOSUB 2000 'route the keyed alphabet
10075 :
10080 PT$="":LOCATE 14,1:Y=14:GOSUB 1000:LOCATE 14,1:PRINT"Enter the plaintext (UPPER CASE, 40-50 letters with/without  spaces, but no":PRINT "punctuation):":LINE INPUT PT$
10090 IF PT$=""THEN PT$=XPT$:LOCATE 15,1:PRINT PT$:PRINT:PRINT TAB(27)"Press <ENTER> to proceed: ";:LINE INPUT QQ$
10100 RP$=PT$:Z=1 'delete any blanks
10110 X=INSTR(Z,PT$," "): IF X THEN Z=X+1:PT$=LEFT$(PT$,X-1)+RIGHT$(PT$,LEN(PT$)-X):GOTO 10110
10120 PT=LEN(PT$)
10125 FOR N=1 TO PT: IF MID$(PT$,N,1)="J" THEN MID$(PT$,N,1)="I":NEXT
10130 LT$=RP$:LOCATE 5,25:COLOR 0,7:PRINT"Plaintext";:COLOR 7,0:PRINT " has"PT"characters:";:GOTO 10150
10140 'A3=0:LT$="":FOR N=1 TO PT:LT$=LT$+CHR$(ASC(MID$(PT$,N,1))OR 32)+"  ":NEXT' space and convert to l.c
10150 FOR N=1 TO 3:LOCATE 5+N,25:PRINT MID$(LT$,1+54*(N-1),54);:NEXT
10155 IF CF=0 THEN GOTO 11160
10160 LOCATE 14,1:Y=14:GOSUB 1000:LOCATE 14,1:COLOR 0,7:PRINT "Ciphertext is:":COLOR 7,0:PRINT:OP$=""
10170 FOR N=1 TO PT:X$=MID$(PT$,N,1)
10180  X=-INT(-(INSTR(H$,X$)/5)):Y=-INT(-(INSTR(V$,X$)/5))
10190  C$=MID$(KS$,X,1)+MID$(KT$,Y,1):
10200 PRINT C$" ";:OP$=OP$+C$+" "
10220 NEXT
10230 GOTO 9260
11000 ' --- double keys ---
11010 LOCATE 5,1:K3$="":PRINT SPC(79):LOCATE 5,1:PRINT"Enter the first side key (UPPER CASE, 5 chars) : ";:X=POS(0):LINE INPUT K3$:IF K3$=""THEN K3$=X3$:LOCATE 5,X:PRINT K3$
11020 K4$="":LOCATE 7,1:PRINT"Enter the second side key (UPPER CASE, 5 chars): ";:X=POS(0):LINE INPUT K4$:IF K4$=""THEN K4$=X4$:LOCATE 7,X:PRINT K4$
11025 FOR N=1 TO 5:IF INSTR(MID$(K3$,N,1),K4$)THEN PRINT"Overlapping letters. Choose another key!":GOTO 11020:
11026 NEXT
11030 K5$="":LOCATE 9,1:PRINT"Enter the first top key (UPPER CASE, 5 chars)  : ";:X=POS(0):LINE INPUT K5$:IF K5$=""THEN K5$=X5$:LOCATE 9,X:PRINT K5$
11040 K6$="":LOCATE 11,1:PRINT"Enter the second top key (UPPER CASE, 5 chars) : ";:X=POS(0):LINE INPUT K6$:IF K6$=""THEN K6$=X6$:LOCATE 11,X:PRINT K6$
11045 FOR N=1 TO 5:IF INSTR(MID$(K5$,N,1),K6$)THEN PRINT"Overlapping letters. Choose another key!":GOTO 11040
11046 NEXT
11050 X$="":FOR N=1 TO 5:X$=X$+MID$(K5$,N,1)+" ":NEXT:S5$=X$ 'space top keys
11060 X$="":FOR N=1 TO 5:X$=X$+MID$(K6$,N,1)+" ":NEXT:S6$=X$ 'space top keys
11070 GOTO 10030
11160 LOCATE 14,1:Y=14:GOSUB 1000:LOCATE 14,1:COLOR 0,7:PRINT "Ciphertext is:":COLOR 7,0:PRINT
11165 J=1:OP$=""
11170 FOR N=1 TO PT:X$=MID$(PT$,N,1)
11180  X=-INT(-(INSTR(H$,X$)/5)):Y=-INT(-(INSTR(V$,X$)/5))
11210  IF J<3 THEN S$=K3$ ELSE S$=K4$
11220  IF J/2=INT(J/2) THEN T$=K5$ ELSE T$=K6$
11230  C$=MID$(S$,X,1)+MID$(T$,Y,1):OP$=OP$+C$+" "
11240  PRINT C$" ";:J=J+1:IF J=5 THEN J=1
11250 NEXT
11310 GOTO 9260
18000 '   --- printer error trap ---
18010 IF ERR=24 OR ERR=25 OR ERR=27 THEN LOCATE 23,1:PRINT SPC(79):LOCATE 23,15: PRINT "Printer not ready; press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79):RESUME
18020 ON ERROR GOTO 0:STOP
19999 '
            ----> examples <----
20000 XPT$="NUMBERS CAN ALSO BE USED AS COORDINATES":K1$="BLACK":K2$="WHITE":X3$=K1$:X4$="HORSE":X5$=K2$:X6$="GRAYS":K9$="KNIGHT":SO$="ACA&U
20050 RETURN
60000 'Non-default data entries corrected; reverse video added; key added to final display; Cm printout added; HELP modified. Oct 7 1989
65000 SAVE"chkboard.enc",A
