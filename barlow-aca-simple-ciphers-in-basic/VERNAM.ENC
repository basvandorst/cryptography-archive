10 '** VERNAM/ENC ** by MB 1987 i2c
20 KEY OFF:CLS: CLEAR :DEFINT A-Z
30 DEF FN XR(A,B)=(A AND (NOT B)) OR ((NOT A) AND B) 'XOR function
40 PRINT TAB(27)"VERNAM ENCIPHERMENT":PRINT TAB(27)"--------------------"
60 PT$="ENOUGH OF THIS NONSENSE; SMILE!"     'example
70 PT=LEN(PT$)
80 KW$="FANCIFUL": KW=LEN(KW$)
90 STARTKEY$=KW$+LEFT$(PT$,PT-KW)    'Put key ahead of plaintext to form autokey
100 PRINT:PRINT"Plaintext  = "PT$: PRINT"Keyword    = "KW$: PRINT"Ciphertext = ";
110 '
     --- do an XOR on each pair in turn ---
120 FOR N=1 TO PT
130  X=ASC(MID$(PT$,N,1))         'get a plain char
140  Y=ASC(MID$(STARTKEY$,N,1))   'get a key char
150  D=FN XR(X,Y)                 'XOR them
160  GOSUB 210                    'convert to hex
170 NEXT                          'go to the next pair
180 LOCATE 22,1:END
200 '
        ----  SUB: DEC TO HEX ----
210 FOR E=1 TO 0 STEP-1:I=INT(16^E+.1):X=INT(D/I)
220   IF X<10 THEN PRINT CHR$(X+48); ELSE PRINT CHR$(X+55); 
230   D=D-X*I
240 NEXT:PRINT" ";:RETURN
65000 SAVE"vernam.enc",A
