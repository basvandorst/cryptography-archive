10 '*** POLLUX.ENC ***  MB Mar 90
20 KEY OFF:CLEAR:DEFINT A-Z:DIM MC(5,26),M(2000),OP(2000)
30 RANDOMIZE TIMER
40 GOSUB 15000                 'load Morse matrix
50 GOSUB 15150                 'set up Morse sounds
60 GOTO 9500
80 :
90 ' --- TRANSLATE TO INTERMEDIATE TEXT-MORSE CODE ---
100 P=0:PRINT "Sample Morse code: ":COLOR 0,7
110 FOR N=1 TO NR                          'NR is # of PT chars incl spaces
120  X$=MID$(P$,N,1)                       'get a plain character
130  T=INSTR(AL$,X$)                       'get it's posn in alphabet (sp=0)
140  K=0                                   'these variables are Caxton Foster's
150  K=K+1
160  P=P+1
170  M(P)=MC(K,T):IF N>=15 GOTO 180        'limit to 15 chars sounded
171  IF M(P)=1 THEN SOUND FREQ,DIT!:SOUND SIL,DIT!:PRINT ".";:GOTO 180
172  IF M(P)=2 THEN SOUND FREQ,DAH!:SOUND SIL,DIT!:PRINT "-";:GOTO 180
173  IF M(P)=3 THEN SOUND SIL, 2*DIT!:PRINT " ";
180  IF M(P)<>3 GOTO 150
190 NEXT:COLOR 7,0
200 P=P+1                                  'add final spacer
210 M(P)=3                                 'make it a 3
240 :
250 PRINT:Y2=CSRLIN:PRINT"Now we will generate the Pollux equivalence table.":PRINT"Press A for automatic generation, X for ACA example: (A*/X) ";:LINE INPUT Q$
260 IF Q$="X" OR Q$="x" THEN GOSUB 20040 ELSE GOTO 1000
270 LOCATE Y2,1:PRINT  SPC(79):PRINT SPC(79):LOCATE Y2+1,1
280 PRINT "Pollux equivalents are:
290 COLOR 0,7:PRINT " 1 2 3 4 5 6 7 8 9 0 ":PRINT " "+POLLUX$;:COLOR 7,0:
292 PRINT"   Dots="DIT$;:PRINT "  Dashes="DAH$;:PRINT "  Spaces="EX$:PRINT
295 :
300 '   --- Convert intermediate text to ciphertext ---
310 PRINT"Ciphertext is:":COLOR 0,7:                 'reverse video
320 FOR N=1 TO P
330  ON M(N) GOTO 350,360,370                        'if its a dot, dash or x..
350  X=INT(RND*4+1):OP=VAL(MID$(DIT$,X,1)):GOTO 380  'get one from the dot bag
360  X=INT(RND*3+1):OP=VAL(MID$(DAH$,X,1)):GOTO 380  'or one from the dash bag
370  X=INT(RND*3+1):OP=VAL(MID$(EX$,X,1))            'or one from the X bag
380  PRINT USING "#";OP;:OP(N)=OP                    'strip the leading blank
385  IF N MOD 5=0 THEN PRINT" ";                     'put in fives
386  IF N MOD 65=0 THEN PRINT                        'and rows of 65
390 NEXT
400 COLOR 7,0                                        'restore video to normal
410 GOTO 9260
490 :
1000 '   --- make up randomised symbol string POLLUX$ from SYMBOL$(n) ---
1010 :
1020 FOR N=1 TO 10:SYMBOL$(N)=" ":NEXT         'start from scratch
1030 :
1040 N=1
1050 Y=INT(10*RND+1)                           'get random 1-10
1070  IF SYMBOL$(Y)>" " THEN GOTO 1050         'if cell is occupied, go again
1080  IF N<5 THEN SYMBOL$(Y)=".":DIT$=DIT$+RIGHT$(STR$(Y),1):GOTO 1110                'first four are to be dots; put them in the dot bag
1090  IF N>=5 AND N<8 THEN SYMBOL$(Y)="-":DAH$=DAH$+RIGHT$(STR$(Y),1):GOTO 1110       'next three are dashes; put them in the dash bag
1100  SYMBOL$(Y)="x":EX$=EX$+RIGHT$(STR$(Y),1)     'last three are X's
1110  IF N<10 THEN N=N+1:GOTO 1050
1120 :
1130 FOR N=1 TO 10:POLLUX$=POLLUX$+SYMBOL$(N)+" ":NEXT  'assemble $ for printing
1140 GOTO 270
4990 :
5000 ' --- misc subs ---
5010 CLS:PRINT DATE$ TAB(29)"* POLLUX ENCIPHERMENT *" TAB(65)"by MIKE BARLOW":PRINT TAB(31)STRING$(19,"-")
5015 RETURN
5020 LOCATE 23,1:PRINT SPC(79);:LOCATE 23,28:PRINT "Press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79);:RETURN
5100 LOCATE 23,1:PRINT SPC(79):LOCATE 23,39:PRINT "end":LOCATE 23,1:END
9240 :
9250 ' --- ENCPRT.STD ---
9260 LOCATE 23,1:PRINT"Do you wish to <P*>rint this for the Cm, do <A>nother, or <Q>uit? ";:LINE INPUT Q$
9270 ON INSTR("PpAaQq",Q$)GOTO 9280,9280,10,10,5100,5100:GOTO 9260
9280 ON ERROR GOTO 18000
9290 WIDTH "lpt1:",255           'needed by GWBASIC
9300 EM(0)=-12971:EM(1)=23813:EM(2)=-28469
9310 X=VARPTR(EM(0))
9320 CALL X
9340 FOR N=1 TO 20:LPRINT:NEXT   'space to bottom of page
9350 LPRINT "Pollux encipherment by Mike Barlow   "DATE$:LPRINT
9360 FOR N=1 TO P:LPRINT USING "#";OP(N);: IF N MOD 5=0 THEN LPRINT " ";
9362  IF N MOD 65=0 THEN LPRINT
9365 NEXT
9370 ON ERROR GOTO 0
9380 LPRINT CHR$(12):GOTO 9260
9490 :
9500 '   ---   start here   ---
9510 GOSUB 5010
9530 AL$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
9560 '   ---   enter data   ---
9610 GOSUB 20000  'pick up default example XPT$
9620 LOCATE 4,1:PRINT"ENTER THE PLAIN TEXT (either case, maintain spaces, suggest <75 chars max):
9630 Y=CSRLIN:LINE INPUT PT$: IF PT$="" THEN PT$=XPT$:LOCATE Y,1:PRINT PT$
9640 NC=LEN(PT$):OP$=""
9650 FOR N=1 TO NC
9660   X$=MID$(PT$,N,1)
9670   IF X$=" " THEN Y$=X$:GOTO 9700              'retain spaces
9680   IF X$<"A" THEN GOTO 9710                    'remove punctuation
9690   Y$=CHR$(ASC(X$) AND 223)                    'force UC
9700   OP$=OP$+Y$
9710 NEXT
9720 P$=OP$:NR=LEN(P$)
9730 PRINT:PRINT"Including spaces, there are"NR"characters in the refined text: (suggest 55-75)":PRINT P$:Y1=CSRLIN
9731 REM **** ACA suggests 155-185 which is ridiculous *****
9790 GOSUB 5020:LOCATE Y1+1,1:GOTO 100
9798 :
15000 '   --- READ IN MORSE MATRIX DOT=1, DASH=2, LETTER SPACE=3 ---
15010 FOR I=0 TO 26
15020   K=1
15030   READ X
15040   MC(K,I)=X
15050   K=K+1
15060   IF X<>3 GOTO 15030
15070 NEXT I:RETURN
15080 '
15090 DATA 3,1,2,3,2,1,1,1,3,2,1,2,1,3,2,1,1,3,1,3,1,1,2,1,3
15100 DATA 2,2,1,3,1,1,1,1,3,1,1,3,1,2,2,2,3,2,1,2,3,1,2,1,1,3
15110 DATA 2,2,3,2,1,3,2,2,2,3,1,2,2,1,3,2,2,1,2,3,1,2,1,3
15120 DATA 1,1,1,3,2,3,1,1,2,3,1,1,1,2,3,1,2,2,3,2,1,1,2,3
15130 DATA 2,1,2,2,3,2,2,1,1,3
15140 '
15150 ' --- set the morse sounds ---
15160 FREQ=600:WPM=24:SIL=32767 'freq,wpm,special silence code
15170 DIT!=21.84/WPM:DAH!=3*DIT!
15180 RETURN
17990 :
18000 '   --- printer error trap ---
18010 IF ERR=24 OR ERR=25 OR ERR=27 THEN LOCATE 23,1:PRINT SPC(79):LOCATE 23,15: PRINT "Printer not ready; press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79):RESUME
18020 ON ERROR GOTO 0:STOP
19990 :
20000 '                    ----  examples ----
20010 XPT$="Luck helps. "                                  'ACA&U p42
20011 XPT$=XPT$+XPT$+XPT$
20012 XPT$=XPT$+XPT$+XPT$
20013 XPT$=LEFT$(XPT$,60):RETURN      'use 81 for max  60 for min test
20014 XPT$="Dear Editor, the recommended length for this cipher is ridiculous.
20015 RETURN
20020 :
20040 POLLUX$="x - . . x . - - x . "                      'ACA&U p42
20050 DIT$="3460":DAH$="278":EX$="159"
20100 RETURN
65000 SAVE"pollux.enc",A
