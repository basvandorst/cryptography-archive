10 '** ROUTE.BAS ** ROUTE TRANSPOSITION AID BY MB version in 1810
15 KEY OFF
20 CLEAR:DEFINT A-Z:CLS:DIM DS(30) 'dim > max (a+b)
30 GOTO 1500
40 ' --- horiz ---
50 PT$=RP$:GOSUB 1810
60 FOR Y=1 TO B
70  FOR X=1 TO A
75    AT=(X-1)*2+(Y-1)*64
80    LOCATE STY+FNY(AT),STX+FNX(AT):PRINT MID$(PT$,X+(Y-1)*A,1)" ";
90  NEXT:PRINT
100 NEXT
120 GOTO 1170
130 ' --- alt horiz ---
140 PT$=RP$:GOSUB 1810
150 FOR Y=1 TO B
160  FOR X=1 TO A
170   IF Y/2=INT(Y/2)THEN 180 ELSE 190 'odd/even switch
180   AT=(A-X+1)*2-2+(Y-1)*64:GOTO 200 'even
190   AT=(X-1)*2+(Y-1)*64 'odd
200   LOCATE STY+FNY(AT),STX+FNX(AT)
205   PRINT MID$(PT$,X+(Y-1)*A,1)" ";
210  NEXT:PRINT
220 NEXT
230 GOTO 1170
240 ' --- verticals ---
250 PT$=RP$:GOSUB 1810
260 FOR X=1 TO A
270  FOR Y=1 TO B
275   AT=(Y-1)*64+(X-1)*2
280   LOCATE STY+FNY(AT),STX+FNX(AT)
290   PRINT MID$(PT$,(X-1)*B+Y,1)" ";
300  NEXT
310 NEXT
320 GOTO 1170
330 ' --- alt verticals ---
340 PT$=RP$:GOSUB 1810
350 FOR X=1 TO A
360  FOR Y=1 TO B
370   IF X/2=INT(X/2)THEN 380 ELSE 390 'odd/even switch
380    AT=(X-1)*2+(B-Y)*64:GOTO 400 'even
390    AT=(Y-1)*64+(X-1)*2
400   LOCATE STY+FNY(AT),STX+FNX(AT)
405    PRINT MID$(PT$,(X-1)*B+Y,1)" ";
410  NEXT
420 NEXT
430 GOTO 1170
440 ' --- spirals ---
450 PT$=RP$:A1=A:B1=B:GOSUB 1810
460 X$=LEFT$(PT$,2*(A1-1)+2*(B1-1)):Y$=RIGHT$(PT$,A*B-(2*(A1-1)+2*(B1-1)))
480 IF ALT=0 THEN GOTO 590
490 '
                ---- anticlockwise spiral ----
500  FOR Y=1 TO B1:AT=64*(Y-1):LOCATE STY+FNY(AT),STX+FNX(AT):PRINT MID$(X$,Y,1)" ";:NEXT 'LHS top-bottom
510   IF A<B AND A1<1 THEN 1170
515   IF A=B AND X=2 AND Y=2 THEN 1170 'odd sided squares
520  FOR X=1 TO A1-1:AT=(B1-1)*64+X*2:LOCATE STY+FNY(AT),STX+FNX(AT):PRINT MID$(X$,B1+X,1)" ";:NEXT  'bottom l-r
530   IF A>B AND B1<=1 THEN 1170
536  IF A<B AND A1<2 THEN 1170 'odd width rectangles
540  FOR Y=1 TO B1-1:AT=(A1-1)*2+(B1-Y-1)*64:LOCATE STY+FNY(AT),STX+FNX(AT):PRINT MID$(X$,A1+B1+Y-1,1)" ";:NEXT       'RHS bottom-top
541 PRINTTAB(70) A1;X;Y;
545  IF A=B AND X=2 AND Y=2 THEN 1170 'even sided squares
546  IF A<B AND A1<=2 THEN 1170 'even width rectangles
550  FOR X=1 TO A1-2:AT=(A1-X-1)*2:LOCATE STY+FNY(AT),STX+FNX(AT):PRINT MID$(X$,A1+B1+B1+X-2,1)" ";:NEXT              'top r-l
570 GOSUB 680:GOTO 500
590 '
                  ---- clockwise spiral ----
600  FOR X=1 TO A1:AT=(X-1)*2:LOCATE STY+FNY(AT),STX+FNX(AT):PRINT MID$(X$,X,1)" ";:NEXT 'top L-R
610 IF A>B AND B1<=1 THEN 1170
615   IF A=B AND X=2 AND Y=2 THEN 1170 'odd sided squares
620  FOR Y=1 TO B1-1:AT=2*(A1-1)+64*Y:LOCATE STY+FNY(AT),STX+FNX(AT):PRINT MID$(X$,A1+Y,1)" ";:NEXT 'RHS top-bottom
635   IF A<B AND A1<=1 THEN 1170
640  FOR X=1 TO A1-1:AT=(B1-1)*64+(A1-X-1)*2:LOCATE STY+FNY(AT),STX+FNX(AT):PRINT MID$(X$,A1+B1+X-1,1)" ";:NEXT 'bottom r-l
645  IF A=B AND X=2 AND Y=2 THEN 1170 'even sided squares
650  FOR Y=1 TO B1-2:AT=(B1-Y-1)*64:LOCATE STY+FNY(AT),STX+FNX(AT):PRINT MID$(X$,A1+B1+A1+Y-2,1)" ";:NEXT 'LHS bottom-top
660 GOSUB 680:GOTO 600
670 '
        --- SUB: now change to the next smaller circle ---
680 STY=STY+1:STX=STX+2:A1=A1-2:B1=B1-2:IF A1<=0 OR B1<=0 THEN 1170
700 X$=LEFT$(Y$,1+2*(A1-1)+2*(B1-1)):Y$=RIGHT$(Y$,A1*B1-(2*(A1-1)+2*(B1-1)))
710 RETURN
730 '                  --- diagonals ---
740 GOSUB 1810:X=1:DC=1:N=0:STX=STX-2:Y=0:RV$="":IF ALT=0 THEN PT$=RP$
750 GOSUB 980             'print 1st char
760 DC=DC+1              'incr diagonal counter
770 IF DC=A+B THEN GOTO 1170
780 GOSUB 920             'step right
790 GOSUB 980             'print
800 RY=Y:RX=X            'remember start of diag
810 GOSUB 880             'calculate # of diag steps DS
820 FOR J=1 TO DS
830   GOSUB 950           'step diagonally down to left
840   GOSUB 980           'print char
850 NEXT
860 Y=RY:X=RX            'restore top-of-diagonal values
870 GOTO 760
880 '
       ---  SUB: calculate # of diag steps reqd ---
890 IF DC<=L THEN DS=DC-1:RETURN    'first few steps
900 IF DC>LL THEN DS=DS-1:RETURN    'end ones
910 DS=L:RETURN                     'middle ones
920 '
         ---  SUB: step right  ---
930 IF X=A THEN Y=RY+1:RETURN
940 X=X+1:RETURN
950 '
         --- SUB: step diagonally ---
960 X=X-1:IF Y=B THEN RETURN
970 Y=Y+1:RETURN
980 '
          ---  SUB: print n+1th char  ---
990 N=N+1
1000 AT=X*2+Y*64:LOCATE STY+FNY(AT),STX+FNX(AT):PRINT MID$(PT$,N,1)" ";:RETURN
1010 '
           --- reverse alt text lines ---
1015 PT$=RP$
1020 X$="":RV$="":FOR DC=1 TO A+B-1
1030  GOSUB 880
1040  DS(DC)=DS+1
1050 NEXT    'now we have the alt line lengths
1060 FOR C=1 TO LP
1070  RV$=RV$+MID$(PT$,LP-C+1,1)
1080 NEXT    'now we have the reversed text
1090 N=0:FOR J=1 TO DC-1
1100  N=N+DS(J) 'count total chars
1110  IF J/2 <> INT(J/2) THEN 1130 'odd diagonals
1120  X$=X$+MID$(PT$,LEN(X$)+1,DS(J)):GOTO 1140 'read fwd
1130  X$=X$+MID$(RV$,LP+1-N,DS(J)): 'read reversed txt
1140 NEXT
1150 PT$=X$:GOTO 730
1160 '
               --- housekeeping ---
1170 LOCATE 19,1:PRINT"This is pattern "SH$":";:LOCATE 20,1:PRINT"Choose: 4-way <R>eadout,";:LOCATE 21,1: PRINT"<N>ew pattern, new <S>hape,";:LOCATE 22,1:PRINT"<Q>uit: ";
1172 LINE INPUT Q$:ON INSTR("RrNnSsQq",Q$)GOTO 1180,1180,1330,1330,1175,1175,1176,1176:GOTO 1170
1175 GOSUB 1810:LOCATE 6,1:GOTO 1620
1176 LOCATE 21,1:PRINT SPC(79):LOCATE 21,38:PRINT"end":END
1180 FOR N8=0 TO 4:LOCATE 19+N8,1:PRINT SPC(27);:NEXT
1185 LOCATE 16,1:PRINT "Reading from the four";:LOCATE 17,1:PRINT "corners in turn gives:"
1190 '
          --- read out the block four ways ---
1200 STX=RSTX:STY=RSTY:FOR N=1 TO 4:X$(N)="":NEXT:FOR Y=1 TO B: FOR X=1 TO A
1210 X$(1)=X$(1)+CHR$(SCREEN (STY+Y-1,STX-2+X*2))
1220 NEXT:NEXT
1230 FOR X=A TO 1 STEP-1:FOR Y=1 TO B
1240 X$(2)=X$(2)+CHR$(SCREEN (STY+Y-1,STX-2+X*2))
1250 NEXT:NEXT:PRINT
1260 FOR Y=B TO 1 STEP-1:FOR X=A TO 1 STEP-1
1270 X$(3)=X$(3)+CHR$(SCREEN (STY+Y-1,STX-2+X*2))
1280 NEXT:NEXT
1290 FOR X=1 TO A:FOR Y=B TO 1 STEP-1
1300 X$(4)=X$(4)+CHR$(SCREEN (STY+Y-1,STX-2+X*2))
1310 NEXT:NEXT:FOR N=1 TO 4:PRINT X$(N):NEXT:X$=""
1320 PRINT:PRINT TAB(30) "Press ENTER to proceed: ";:LINE INPUT Q$
1330 STX=RSTX:STY=RSTY:GOSUB 1810:LOCATE 20,1:GOTO 1720
1500 '
               ---- initialisation ----
1510 DEF FN Y(A9)=INT(A9/64):DEF FN X(A9)=A9-64*FNY(A9) 'conv PRINT@ A9 to Y,X
1520 GOSUB 2000 'get example
1530 GOSUB 1810
1540 LOCATE 4,33:PRINT "By Mike Barlow"
1550 '
                    ---- start here ----
1560 PT$="":PRINT:PRINT"Enter the text (UPPER CASE, with or w/o blanks): ";:LINE INPUT PT$
1570 IF PT$=""THEN PT$=XPT$:PRINT PT$:Y9=CSRLIN:X=POS(0):IF LEN(SO$)>1 THEN LOCATE 1,68:PRINT SO$: LOCATE Y9,X
1575 'LOCATE Y9,X
1580 '
             ---- delete blanks in plaintext ----
1590 Z=1
1600 X=INSTR(PT$," "):IF X>0 THEN Z=X: PT$=LEFT$(PT$,X-1)+RIGHT$(PT$,LEN(PT$)-X):GOTO 1600
1610 RP$=PT$:LP=LEN(PT$):PRINT"   ("+RIGHT$(STR$(LP),LEN(STR$(LP))-1)+" active chars)."       'add char count
1620 '
         ---- factorise LP to get rectangle size ----
1630 PRINT:PRINT"FACTORS ARE:
1640 FOR N=3 TO 15
1650  IF LP/N = INT(LP/N) THEN PRINT N"x"LP/N,;
1660 NEXT
1670 LOCATE 14,1:PRINT"(1) - To build a rectangle or square, what is the length of the horizontal side       (if a rectangle, try both formats): ";:INPUT SL:IF SL<1 THEN 1670
1680 X=LP/SL:IF LP/SL<>INT(LP/SL)THEN PRINT"Try again.":GOTO 1670
1690 IF X=SL THEN 1700 ELSE PRINT TAB(4)"You have chosen a rectangle"SL"wide by"LP/SL"high. Is that correct? (Y*/N): ";:LINE INPUT QQ$:IF QQ$="N" OR QQ$="n" THEN 1670 ELSE A=SL:B=LP/SL:GOTO 1710
1700 PRINT"You have chosen a square of "SL"x"SL". Is that correct? (Y*/N): ";:X=POS(0):Y=CSRLIN:LINE INPUT QQ$:IF QQ$="N" OR QQ$="n"THEN 1670 ELSE LOCATE Y,X:PRINT"Y":A=SL:B=SL
1710 IF A<B THEN L=A-1: LL=B ELSE L=B-1: LL=A
1714 STY=4:RSTY=STY 'sty is row for top lh corner
1715 ST=40-SL:STX=ST:RSTX=STX 'stx is top lh corner tab posn
1716 ON ERROR GOTO 20000
1720 '
1730 PRINT:PRINT"(2) - Now choose the basic shape for the Route:
1740 PRINT TAB(7)"1- Horizontal"TAB(23)"2- Alt Horiz"TAB(39)"3- Vertical"TAB(55)"4- Alt Vert":PRINT TAB(7)"5- C/W Spiral"TAB(23)"6- AC/W Spiral"TAB(39)"7- Diagonal"TAB(55)"8- Alt Diag:";:INPUT SH$:IF INSTR("12345678",SH$)=0 THEN GOTO 1740
1750 IF SH$="6" OR SH$="8"THEN ALT=1 ELSE ALT=0 'set alternate diag flag
1760 ON INSTR("12345678",SH$)GOTO 40,130,240,330,440,440,730,1010
1770 STOP
1800 '        ---- misc SUBS ----
1810 CLS:PRINT DATE$;TAB(23) "* ROUTE TRANSPOSITION CIPHER AID *"TAB(69)"Version I56":PRINT TAB(25)STRING$(30,"-"):RETURN
1820 FOR N9=1 TO 4 :PRINT SPC(79):NEXT:RETURN
2000 '
           ---- examples ----
2010 'XPT$="THERE ARE MANY ROUTES":SO$="ACA&U P29":RETURN
2020 XPT$="MMVAT ARYNC FBOCA OECEY EYESD ARIPO YRRTD YSBMD OTPVY SBIAO AEEEM AYIOE EONYL GRNWN MTUNF NDRWR RUADO CRERA MPAAY SBPSH":SO$="WABBIT":
2030 XPT$="EUGWS BESEW IURII RFLAT LORIF GUNTL IL":SO$="ND87E3":'AH + AD
2040 XPT$="MYELP DTDAH IEDRL ILANI ANOBR MWCES WITGA REAEO AARAISOARVONNAND":SO$="PCIV P10"'PRAC CRYP VOLIV P10":'AV 7X8
2050 'XPT$="PRLER LSSEI ADSMH GYCRM CEIOW EUSSN MITSN GSGIN SMNPBNIUIR UGTMI TURNA AGWS":SO$="MA87 E5":RETURN 'CW FROM CENTRE
2060 'XPT$="ABCDEFGHIJKLMNOPQRSTUVWXY" '25
2061 XPT$="THARY RESTU AMREE ENO":SO$="nacu&u P49":RETURN
2070 'XPT$="ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvw":'49
2080 'XPT$="OUSNW OHTDE TSTES GTRER ACTSC HHACA HTLLR GEHTL OTEGY OEAMA USTNU NGCUA HSPAI TODRM
2100 RETURN
20000 RESUME NEXT
60000 'was I9P
64000 '
64001 'This program was built on a TRS80 with a 64x16 screen. Hence the use of FNx and FNy to change from PRINT@ statements to LOCATEs. Variable ST is used to determine the "Start" PRINT@ position to maintain a square or rectangle
64002 'centered on the screen. This has been converted to STX and STY which have been left outside the FNx and FNy for convenience.
64003 'All other variables are unchanged. Scroll locking was abandoned as I was unable to prevent a CLS from clearing the top lines of a VIEW PRINT field. SUBs 1800 and 1820 are a pathetic attempt to duplicate the PRINT CHR$(30) and (31)
64004 'statements of NEWDOS BASIC. **** GWBASIC IS AWFUL!!!! ****
65000 SAVE"route.enc",A
