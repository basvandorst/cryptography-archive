10 '*** SIMSUB.ENC *** Simple Sub encipherer MB Mar 90
20 KEY OFF:CLEAR:DEFINT A-Z:DIM SC(90)
30 NA$="SIMPLE SUBSTITUTION ENCIPHERMENT":LNA=32
40 AL$="ABCDEFGHIJKLMNOPQRSTUVWXYZ" 'all 26
50 GOTO 1000
100 ' --- SUB: check for singletons in XT$ ---
101 SC=0
105 XT$=PT$:GOSUB 800                 'replace punctuation by blanks
110 XT$=" "+XT$+" "                   'add spaces at both ends
120 FOR N=65 TO 90
130  Z=1
140  X=INSTR(Z,XT$," "+CHR$(N)+" ")
150  IF X=0 THEN 190
160  SC(N)=SC(N)+1                    'SC(n) is number of chr$(n) singles
170  IF SC(N)=1 THEN SC=SC+1          'SC is number of singletons
180  Z=X+1:GOTO 140
190 NEXT
200 IF SC>3 THEN BEEP:PRINT:PRINT"OUTSIDE ACA LIMITS."SC"singletons in use; ACA suggests 3 max.":GOSUB 5030:IF Q$="R" OR Q$="r" THEN GOTO 9620
210 RETURN
215 :
220 ' --- SUB: check number of proper nouns in PT$ ---
225 PNC=0
230 Z=1
240 X=INSTR(Z,PT$,"*")
250 IF X=0 THEN 270
260 PNC=PNC+1:Z=X+1:GOTO 240
270 IF PNC>3 THEN BEEP:PRINT:PRINT"OUTSIDE ACA LIMITS."PNC"proper nouns marked with asterisk. ACA suggests 3 max.":GOSUB 5030:IF Q$="R" OR Q$="r" THEN 9620
280 RETURN
300 '   --- SUB: check number of different letters in PT$ ---
305 DLC=0
310 FOR N=65 TO 90
320  X=INSTR(PT$,CHR$(N))
330  IF X THEN DLC=DLC+1
340 NEXT
350 IF DLC<18 THEN BEEP:PRINT:PRINT "OUTSIDE ACA LIMITS."DLC"different letters in plaintext. ACA suggests at least 18.":GOSUB 5030:IF Q$="R" OR Q$="r" THEN GOTO 9620
360 RETURN
370 :
400 '   --- SUB: check for text length NR ---
410 IF NR>=MINL AND NR<=MAXL THEN RETURN
420 IF NR<85 OR NR>140 THEN BEEP:PRINT:PRINT "OUTSIDE ACA LIMITS.";NR;"letters in plaintext.":PRINT"ACA requests never less than 85 and never more than 140.":GOTO 440
430 BEEP:PRINT "OUTSIDE ACA LENGTH LIMITS."NR"letters in plaintext; ACA suggests"MINL"to"MAXL:
440 GOSUB 5030:IF Q$="R" OR Q$="r" THEN 9620
450 RETURN
500 '   --- SUB: remove any duplicate letters in XT$ and force U.C ---
510 OP$="":FOR N=1 TO LEN(XT$)
520   DU$=MID$(XT$,N,1):IF DU$=" " THEN 560   'remove spaces
530   DU$=CHR$(ASC(DU$) AND 223)              'force UC
540   IF INSTR(OP$,DU$) THEN GOTO 560
550   OP$=OP$+DU$
560 NEXT:XT$=OP$
570 RETURN
600 '   --- SUB: check no letter is same as its substitute ---
610 FOR N=1 TO 26
620  IF MID$(PAL$,N,1)=MID$(CAL$,N,1) THEN BEEP:PRINT:PRINT "ALPHABETS NOT ACCEPTABLE. At least one letter is the same as its substitute.":GOSUB 5020:FLAG=1:RETURN
630 NEXT:RETURN
690 :
700 '   --- SUB:get alphabet offset (OSXAL$ relative to RAL$) ---
710 LOCATE 7,1:Q$=""
720 PRINT "The keyword can be offset from the start of its alphabet." :PRINT :IF K<3 THEN PRINT "The keyword is to start opposite which letter of the unkeyed alphabet? (A-Z):";:GOTO 735
730 PRINT "The keyword is to start opposite which letter of the other alphabet? (A-Z):";
735 Y=CSRLIN:X=POS(0):LINE INPUT Q$
740 IF LEN(Q$)=0 THEN Q$=XOS$:LOCATE Y,X:PRINT Q$     'default example
750 OS$=CHR$(ASC(Q$) AND 223)                         'force U.C
760 X =INSTR(RAL$,OS$): IF X=0 THEN BEEP:PRINT:PRINT"ERROR. Must be A-Z.":GOSUB 5020:GOTO 710
770 OSXAL$=RIGHT$(XAL$,X-1)+LEFT$(XAL$,27-X)
780 RETURN
790 :
800 ' --- SUB: change all punctuation in XT$ to spaces ---
810 LX=LEN(XT$):P$=""
820 FOR N=1 TO LX
830   X$=MID$(XT$,N,1)
835  IF ASC(X$)=39 THEN 850       'eliminate apostrophes
840   IF ASC(X$)<=64 THEN MID$(XT$,N,1) = " "
850 NEXT
860 RETURN
899 :
900 '   --- SUB: print a summary ---
905 A1=40    'tab figure
910 PRINT"Number of letters in plaintext is"NR;:PRINT TAB(A1)"(ACA suggests"MINL"-"MAXL;:PRINT"(+20% if foreign)
920 PRINT "Number of different letters is"DLC;:PRINT TAB(A1)"(ACA suggests 18 min)
930 PRINT "Number of singletons is"SC;:PRINT TAB(A1)"(ACA suggests  3 max)
940 PRINT "Number of proper names is"PNC;:PRINT TAB(A1)"(ACA suggests  3 max)
950 RETURN
999 :
1000 '   ---   start here   ---
1010 GOSUB 5010
1020 PRINT:PRINT TAB(33)"By Mike Barlow"
1030 LOCATE 7,1: PRINT"Is this to be an  ";:COLOR 0,7:PRINT" <A>RISTOCRAT ";:COLOR 7,0:PRINT"      (word divisions and punctuation retained)
1040 LOCATE 9,13: PRINT "or a ";:COLOR 0,7:PRINT " <P>ATRISTOCRAT ";:COLOR 7,0:PRINT"     (5-letter groups of letters only)?"
1050 LOCATE 11,1:PRINT "Choose A* or P: ";:LINE INPUT Q$
1060 IF Q$="P" OR Q$="p" THEN GOSUB 9020:GOTO 1080
1070 GOSUB 9010
1080 GOSUB 5010
1100 :
1200 ' --- get key type ---
1210 A1=12:LOCATE 7,1:PRINT "Select the type of key:":PRINT
1220 PRINT TAB(A1) "K1 - keyed plain, normal cipher alphabets
1230 PRINT TAB(A1) "K2 - normal plain, keyed cipher alphabets
1240 PRINT TAB(A1) "K3 - keyed plain, keyed cipher alphabets (same key)
1250 PRINT TAB(A1) "K4 - keyed plain, keyed cipher alphabets (different keys)
1260 PRINT:PRINT"Choose 1*-2-3-4: ";:Y=CSRLIN:X=POS(0):LINE INPUT Q$:IF LEN(Q$)=0 THEN LOCATE Y,X:PRINT"1"
1265 IF INSTR("1234",Q$)=0 THEN GOSUB 5010:GOTO 1200
1270 PRINT:PRINT"For a Mixed or Variant key (with manual alphabet entry), enter M (just ENTER":PRINT "otherwise) ";:LINE INPUT QQ$
1280 ON INSTR("1234",Q$) GOTO 1310,1320,1330,1340
1290 GOTO 1270
1310 K=1: K$= " K1 ":XOS$="G":GOTO 1350
1320 K=2: K$= " K2 ":XOS$="G":GOTO 1350
1330 K=3: K$= " K3 ":XOS$="F":GOTO 1350
1340 K=4: K$= " K4 ":XOS$="E"
1350 IF QQ$="M" OR QQ$="m" THEN K$=K$+"M/V ":GOSUB 20050:GOSUB 5010:GOTO 1500
1360 GOSUB 5010:GOSUB 20000                     'pick up default data
1370 GOTO 2000
1400 :
1500 ' --- manual keyed alphabet entry for Ms and Vs ---
1520 LOCATE 7,1: PRINT "For MIXED and VARIANT keys, you must enter your own 26-letter keyed alphabet ":PRINT"(two in the case of a K4).
1530 PRINT:PRINT TAB(10) "Enter the alphabet below (in UPPER case), or press <ENTER> for the built-in     example, or <Q> to quit)":PRINT
1540 LINE INPUT KAL1$:IF LEN(KAL1$)=0 THEN KAL1$=XKAL1$:PRINT KAL1$
1550 IF KAL1$="Q" OR KAL1$="q" THEN RUN
1560 IF LEN(KAL1$)<>26 THEN PRINT "That is not 26 letters. Try again.":GOSUB 5020:GOSUB 5010:GOTO 1500
1570 XT$=KAL1$:GOSUB 500:KAL1$=XT$           'check dupes, force UC
1590 LOCATE 18,1:PRINT "Your alphabet is accepted. Press <ENTER> to proceed, <N> for new alphabet: ";:LINE INPUT Q$
1600 IF Q$="N" OR Q$="n" THEN GOSUB 5010:GOTO 1500
1610 IF K<4 THEN GOTO 2150 ELSE GOSUB 5010
1620 :
1630 LOCATE 10,1:PRINT TAB(10) "Enter the second alphabet below (in UPPER case), or press <ENTER> for the built-in example, or <Q> to quit)":PRINT
1640 LINE INPUT KAL2$:IF LEN(KAL2$)=0 THEN KAL2$=XKAL2$:PRINT KAL2$
1650 IF KAL2$="Q" OR KAL2$="q" THEN RUN
1660 IF LEN(KAL2$)<>26 THEN PRINT "That is not 26 letters. Try again.":GOSUB 5020:GOSUB 5010:GOTO 1630
1670 XT$=KAL2$:GOSUB 500:KAL2$=XT$           'check dupes, force UC
1690 LOCATE 18,1:PRINT "Your alphabet is accepted. Press <ENTER> to proceed, <N> for new alphabet: ";:LINE INPUT Q$
1700 IF Q$="N" OR Q$="n" THEN GOSUB 5010:GOTO 1630
1710 GOTO 2150
1990 :
2000  '   ---  enter the first keyword KW1$  ---
2010 KW1$="":LOCATE 7,1:PRINT"ENTER THE KEYWORD (UPPER CASE, no spaces): ";:X=POS(0):Y=CSRLIN:LINE INPUT KW1$:IF KW1$=""THEN KW1$=XKW1$:LOCATE Y,X:PRINT KW1$
2020 RKW1$=KW1$:XT$=KW1$:GOSUB 500:KW1$=XT$             'check for duplicates
2030 PRINT:PRINT "Adjusted Keyword is: "KW1$
2040 KW$=KW1$:GOSUB 2400:KAL1$=KAL$
2042 LOCATE 11,1:PRINT "Keyed  alphabet is: "KAL1$:GOSUB 5030
2047 IF Q$="R" OR Q$="r" THEN GOSUB 5010:GOTO 2000
2050 IF K<4 THEN GOSUB 5020:GOTO 2150
2060 :
2070 '   --- enter the second keyword for K4s ---
2080 KW2$="":LOCATE 13,1:PRINT"ENTER THE SECOND KEYWORD (UPPER CASE, no spaces): ";:X=POS(0):Y=CSRLIN:LINE INPUT KW2$:IF KW2$=""THEN KW2$=XKW2$:LOCATE Y,X:PRINT KW2$
2090 RKW2$=KW2$:XT$=KW2$:GOSUB 500:KW2$=XT$             'check for duplicates
2100 PRINT:PRINT "Adjusted Keyword is: "KW2$
2110 KW$=KW2$:GOSUB 2400:KAL2$=KAL$
2120 LOCATE 17,1:PRINT "Keyed  alphabet is: "KAL2$:GOSUB 5030
2130 IF Q$="R" OR Q$="r" THEN GOSUB 5010:GOTO 2000
2140 :
2150 GOSUB 5010
2200 ON K GOTO 2210,2220,2230,2240,2250
2210 RAL$=AL$:XAL$=KAL1$:GOSUB 700:PAL$=OSXAL$:CAL$=AL$:GOTO 2270
2220 RAL$=AL$:XAL$=KAL1$:GOSUB 700:PAL$=AL$:CAL$=OSXAL$:GOTO 2270
2230 RAL$=KAL1$:XAL$=KAL1$:GOSUB 700:PAL$=KAL1$:CAL$=OSXAL$:GOTO 2270
2240 RAL$=KAL1$:XAL$=KAL2$:GOSUB 700:PAL$=KAL1$:CAL$=OSXAL$
2250 :
2270 PRINT:PRINT"Plain  alphabet is: "PAL$
2280  PRINT"Cipher alphabet is: "CAL$
2290 GOSUB 600:IF FLAG=1 THEN FLAG=0:GOTO 2150 'check no letter is same as subst
2300 GOSUB 5020:GOTO 9600
2390 :
2400 '  ---  SUB: make up keyed alphabet KAL$  ---
2410 FOR I=65 TO 90
2420  IF INSTR(KW$,CHR$(I))=0 THEN KW$=KW$+CHR$(I)
2430 NEXT:KAL$=KW$
2450 RETURN
3000 '   --- perform the substitution PT$>CT$ ---
3010 GOSUB 5010: LOCATE 4,1:COLOR 0,7:PRINT " Plaintext is: ":COLOR 7,0:PRINT RPT$
3011 IF QQ$ ="M" OR QQ$="m" THEN GOTO 3014
3012 PRINT:COLOR 0,7:PRINT K$"Keyword is: ";:COLOR 7,0:PRINT " "RKW1$;:IF K=4 THEN PRINT"    Second keyword is: "RKW2$
3014 PRINT:PRINT"Plain  alphabet is: "PAL$
3015 PRINT"Cipher alphabet is: "CAL$
3018 OP$=""
3020 FOR N=1 TO LEN(PT$)
3030  X$=MID$(PT$,N,1)
3040  IF ASC(X$)<65 THEN Y$=X$:GOTO 3070
3050  X=INSTR(PAL$,X$)         'get posn of PT letter in plain alphab.
3060  Y$=MID$(CAL$,X,1)        'get corresp CT letter in cipher alphab.
3070  OP$=OP$+Y$
3080 NEXT
3090 PRINT'"OP$="OP$
3100 :
3110 ' --- print the final ciphertext nicely ---
3115 COLOR 0,7:PRINT" Ciphertext is: ":COLOR 7,0
3120 IF SPACING=1 GOTO 3500
3125 '   --- print the PAT in 5's ---
3130 CT$=""
3140 FOR N=1 TO LEN(OP$)
3150  CT$=CT$+MID$(OP$,N,1)
3160  IF N/5=INT(N/5) THEN CT$=CT$+" "
3170 NEXT
3190 PRINT CT$:GOTO 4000
3500 :
3510 NOP$="":LL=65                                  '"Cryptogram" chars/line
3530 N=1                                            'count lines
3540 X$=MID$(OP$,1+(N-1)*LL,LL)                     'get nth line of text
3545  IF LEN(X$)<LL THEN Y$=X$:N=4:GOTO 3570        'ok, this is last line
3550  RHS$=RIGHT$(X$,1)                             'get RH character
3552  IF RHS$=" " THEN Y$=X$:GOTO 3570              'ok, no hyphen needed
3555  IF MID$(X$,LL-1,1)=" " THEN GOTO 3700         'check last-but-one char
3560  Y$=LEFT$(X$,LEN(X$)-1)+"="                    'nok, add "hyphen"
3565  OP$=LEFT$(OP$,N*LL-1)+" "+RIGHT$(OP$,LEN(OP$)-N*LL+1) 'incr length of op$
3570 NOP$=NOP$+Y$                                   'make up new output
3580 PRINT Y$
3590 IF N<4 THEN N=N+1:GOTO 3540                    'next line
3600 OP$=NOP$
3610 PRINT:GOSUB 900:GOTO 4000
3690 :
3700  Y$=LEFT$(X$,LEN(X$)-1)+" ":GOTO 3565          'no hyphen if RHS was 1st                                                        letter of word
3990 :
4000 '   --- ENCPRT.STD ---
4010 LOCATE 23,1:PRINT"      Do you wish to <P*>rint this for the Cm, do <A>nother, or <Q>uit? ";:LINE INPUT Q$
4020 ON INSTR("PpAaQq",Q$)GOTO 4030,4030,10,10,5100,5100:GOTO 4010
4030 BEEP:STOP
4031 ON ERROR GOTO 18000
4040 WIDTH "lpt1:",255           'needed by GWBASIC
4050 EM(0)=-12971:EM(1)=23813:EM(2)=-28469
4060 X=VARPTR(EM(0))
4070 CALL X
4080 FOR N=1 TO 20:LPRINT:NEXT   'space to bottom of page
4090 LPRINT NA$" by Mike Barlow   "DATE$:LPRINT
4100 LPRINT CT$
4110 ON ERROR GOTO 0
4120 LPRINT CHR$(12):GOTO 4010
4990 :
5000 ' --- misc subs ---
5010 CLS:PRINT DATE$ TAB(40-LNA/2);NA$ :PRINT TAB(40-LNA/2)STRING$(LNA,"-"):IF K THEN LOCATE 1,65:COLOR 0,7:PRINT K$;:COLOR 7,0
5015 RETURN
5020 LOCATE 23,1:PRINT SPC(79);:LOCATE 23,28:PRINT "Press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79);:RETURN
5030 Q$="":LOCATE 23,1:PRINT SPC(79);:LOCATE 23,15:PRINT "Press <ENTER> to proceed anyway, <R> to redo last entry: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79);:RETURN
5100 LOCATE 23,1:PRINT SPC(79):LOCATE 23,39:PRINT "end":LOCATE 23,1:END
5101 LOCATE 23,1:PRINT SPC(79):LOCATE 23,39:PRINT "end":LOCATE 23,1:END
9000 ' --- data ---
9010 NA$ = "ARISTOCRAT ENCIPHERMENT": LNA=23:MINL=75:MAXL=100:SPACING=1:RETURN
9020 NA$ = "PATRISTOCRAT ENCIPHERMENT": LNA=25:MINL=90:MAXL=105:SPACING=0:RETURN
9490 :
9600 ' --- get plaintext ---
9610 GOSUB 20000  'pick up default example XPT$
9620 GOSUB 5010:LOCATE 7,1:PRINT"ENTER THE PLAIN TEXT (either case, with or w/o spaces, ACA limits 50-75 letters max):"
9630 Y=CSRLIN:LINE INPUT PT$: IF PT$="" THEN PT$=XPT$:LOCATE Y,1:PRINT PT$
9635 RPT$=PT$                 'remember original entry
9640 :
9650 ' --- for PATS: change PT$ to unspaced U.C P$ ---
9660 NC=LEN(PT$):P$=""
9670 FOR N=1 TO NC
9680   IF ASC(MID$(PT$,N,1))<=64 THEN 9700 'remove any spaces, numbers, punctn.
9690   P$=P$+CHR$(ASC(MID$(PT$,N,1)) AND 223)  'force U.C
9700 NEXT:NR=LEN(P$)
9710 IF SPACING=0 THEN PRINT:PRINT"For "NA$" the text has been converted to: ":PRINT:PRINT P$:PT$=P$:GOTO 9840
9720 :
9730 '   --- for A's: change a-z in PT$ to A-Z, retaining all else ---
9740 P$="":FOR N=1 TO NC
9750   X$=MID$(PT$,N,1)
9790   IF INSTR("abcdefghijklmnopqrstuvwxyz",X$) THEN MID$(PT$,N,1)=CHR$(ASC(X$)-32)
9800 NEXT
9810 PRINT:PRINT"For "NA$" the text has been converted to: ":PRINT:PRINT PT$
9820 GOSUB 100   'check singletons
9830 GOSUB 220   'check proper nouns
9840 GOSUB 300   'check # difft letters
9850 GOSUB 400   'check for length
9900 GOTO 3000
17999 STOP
18000 '   --- printer error trap ---
18010 BEEP:IF ERR=24 OR ERR=25 OR ERR=27 THEN LOCATE 23,1:PRINT SPC(79):LOCATE 23,15: PRINT "Printer not ready; press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79):RESUME
18020 ON ERROR GOTO 0:STOP
20000 '                    ----  examples ----
20010 XPT$="Word divisions may be kept, but I, *Mike *Barlow, like *Jean's idea; don't you?"
20011 XPT$="It is axiomatic that no matter when you take your vacation, or where (in *Canada or the *United *States), the local *PBS station will always be having its membership drive and your favourite programs will have been pre-empted!"
20012 XPT$="I find that no matter when I take my   vacation, the local *PBS station will always be having its membership drive and my favourite programs will have been pre-empted!"
20030 XKW1$="muggins"
20040 XKW2$="simultaneously"
20050 XKAL1$="RBKVODLWMFPXAGQYNHSZCITEJU" 'aca&u p22 1st mixed keyed alphabet
20060 XKAL2$="AGQYCITEJUMFPXNHSZODLWRBKV" 'aca&u p22 2nd mixed keyed alphabet
20100 RETURN
64000 'mod record: 102/2048/3490 dleted; 700 corr [03/18/90]
65000 SAVE"simsub.enc",A
