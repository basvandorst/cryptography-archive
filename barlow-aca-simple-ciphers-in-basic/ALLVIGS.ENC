10 '** ALLVIGS.ENC ** BY MB 1987 June 27 incl Porta using strings
20 'does all varieties of polyalphas
40 GOTO 2010 'initialise and get example
50 '
330 '
  --- SUB: elim blanks and force Upper case in ZZ$  ---
335 OP$=""
340 FOR N=1 TO LEN(ZZ$)
345  X$=MID$(ZZ$,N,1):IF X$=" " THEN 360
350  X$=CHR$(ASC(MID$(ZZ$,N,1)) AND 223)
355  OP$=OP$+X$
360 NEXT
365 ZZ$=OP$:OP$="":RETURN
370 '
                    ---- misc subs ----
400 LOCATE 24,1:PRINT SPC(79);:LOCATE 24,24:PRINT"Press <ENTER> to proceed: ";:LINE INPUT QQ$:LOCATE 24,1:PRINT SPC(79);:RETURN
600  CLS:PRINT DATE$ TAB(28)TI$+" ENCIPHERMENT":PRINT TAB(28)STRING$(LEN(TI$)+13,"-")
610  IF SO$>"" THEN LOCATE 1,55:PRINT "Source = "SO$:RETURN ELSE RETURN
800 '
830  LOCATE 22,38:PRINT "end";:END
835 GOTO 835
840 '
           ---- process the keyword ----
850 LOCATE 12,1:PRINT SPC(79)
855 LL=INT(LP/15):HL=INT(LP/12)
860 KW$="":PRINT"Enter the keyword (either case; ACA limits with this text"LL"to"HL"chars): ";
870 IF XKW$>""THEN PRINT"(Press ENTER for "XKW$"): ";
880 Y=CSRLIN:X=POS(0):LINE INPUT KW$
890 IF KW$=""AND XKW$>""THEN KW$=XKW$:LOCATE Y,X:PRINT KW$
900 IF KW$=""THEN 880
910 ZZ$=KW$:GOSUB 330:KW$=ZZ$    'force upper case
920 KW=LEN(KW$)
921 IF TY$="5"THEN 1140
930 '
       ---- display on screen in KWs horizontally ----
940 ON ERROR GOTO 1030
945 GOSUB 600                              'title
950 LOCATE 1,58:PRINT"Key = "KW$;:LOCATE 3,1:PRINT "Plaintext: ":PRINT RP$:PRINT:LOCATE 7,1: PRINT"Plaintext in groups:": LOCATE 12,1:PRINT"Equivalent ciphertext:":LOCATE 17,1: PRINT"And in fives:"
960 FOR N=0 TO LP/KW
970  FOR M=1 TO KW
980    X$=MID$(PT$,M+N*KW,1):IF X$="" THEN 1020
981    ON INSTR("1234",TY$) GOSUB 991,993,995,997:GOTO 1000
991    X=ASC(X$)+ASC(MID$(KW$,M,1))-65:IF X>90 THEN X=X-26
992    RETURN:'Vig
993    X=ASC(MID$(KW$,M,1))-ASC(X$)+65:IF X<65 THEN X=X+26
994    RETURN:'Beau
995    X=ASC(X$)-ASC(MID$(KW$,M,1))+65:IF X<65 THEN X=X+26
996    RETURN:'Var
997    X=ASC(X$)+ASC(MID$(KW$,M,1))-48:IF X>90 THEN X=X-26
998    RETURN:'Grons
1000 '
1010    CG$=CG$+CHR$(X)      'cipher to be grouped
1020 NEXT:NEXT:GOTO 1040
1030 RESUME 1040
1040 ON ERROR GOTO 0
1050 '
1060 '  ---- print in groups/fives ----
1070 LOCATE 8,1: FOR N=1 TO -INT(-LP/KW):PRINT MID$(PT$,1+(N-1)*KW,KW)+" ";:NEXT
1080 LOCATE 13,1:FOR N=1 TO -INT(-LP/KW):PRINT MID$(CG$,1+(N-1)*KW,KW)+" ";:NEXT
1090 LOCATE 18,1: OP$="":FOR N=1 TO -INT(-LEN(CG$)/5):OP$ = OP$+MID$(CG$,1+(N-1)*5,5)+" ":NEXT:PRINT OP$
1120 GOTO 9260
1130 '
     ---- display PORTA on screen in KWs horizontally ----
1140 GOSUB 600
1150 LOCATE 1,55:PRINT"Key = "KW$;:LOCATE 3,1:PRINT "Plaintext: ":PRINT RP$:PRINT:LOCATE 7,1: PRINT"Plaintext in groups:": LOCATE 12,1:PRINT"Equivalent ciphertext:":LOCATE 17,1: PRINT"And in fives:"
1160 FOR N=0 TO LEN(PT$)/KW
1170  FOR M=1 TO KW
1180    X$=MID$(PT$,M+N*KW,1)
1190    Y=ASC(MID$(KW$,M,1))-64:IF Y/2<>INT(Y/2) THEN Y=Y+1 'pair the keys e.g                                                             A&B=2;O&P=16;Y&Z=26
1200    Y=Y/2                                    'use only 1/2 the Vig tableau
1210    H$=MID$(NL$,Y,13)                        'get horiz partial albet
1220    X=INSTR(H$,X$)                           'find posn of PT in H$
1230    IF X=0 THEN X=INSTR(TL$,X$):TL=1 ELSE TL=0 'if not in H$ must be in TL$
1240    IF TL THEN OP$=MID$(H$,X,1):GOTO 1270
1250    OP$=MID$(TL$,X,1)
1270    CG$=CG$+OP$
1280    KK=KK+1:IF KK=LP THEN 1300                'count chars
1290 NEXT:NEXT
1300 GOTO 1050
2000 '
                 ---- initialisation ----
2010 KEY OFF:CLEAR :DEFINT A-Z:GOSUB 10000
2030 DIM F%,L%,I%,C%,P%,N%,S%,D%,X%,A1%,X$,Q$,N1%,M%,J%,C$,KW$,K%,CT$,KW%,T!,PH!,LC%,K$,XK$,SO$,XS$,CC$,SK%,KK%,XC$,LK%,PK$,DT$,C1%,RT$,K1%,XS%,XL%,X%,E%,T%,CT$,M%
2040 AL$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
2050 LK$="ACEGIKMOQSUWY":RK$="BDFHJLNPRTVXZ":TL$="ABCDEFGHIJKLM":NL$="NOPQRSTUVWXYZNOPQRSTUVWXYZ"
2060 '
2070 CLS:PRINT DATE$ TAB(25)"VIGENERE-TYPE ENCIPHERMENT":PRINT TAB(25)STRING$(26,"-")
2080 DIM X(20,26),E(26),M!(30),M(30),FC(16)
2090 PRINT:PRINT TAB(32)"By Mike Barlow ":PRINT
2120 A1=34:PRINT:PRINT TAB(A1-14)"Do you want a (1) Vigenere":PRINT TAB(A1)"(2) Beaufort":PRINT TAB(A1)"(3) Variant":PRINT TAB(A1)"(4) Gronsfeld":PRINT TAB(A1)"(5) Porta : ";:LINE INPUT TY$
2125 IF TY$="" THEN TY$="1"
2130 IF INSTR("12345",TY$)=0 THEN 2120
2140 ON INSTR("12345",TY$)GOSUB 10010,10020,10030,10040,10050
2150 GOSUB 600
2160 '
                  ---- keyboard entry ----
2170 LOCATE 5,1:PT$="":PRINT:PRINT"Enter the plaintext (either case, with or without spaces, but no punctuation): ":LINE INPUT PT$
2180 IF PT$=""THEN PT$=XPT$:PRINT PT$;
2185 RP$=PT$   'remember original text
2190 ZZ$=PT$:GOSUB 330:PT$=ZZ$        'delete blanks in text
2200 LP=LEN(PT$):CP$="("+RIGHT$(STR$(LP),LEN(STR$(LP))-1)+" chars).":PRINT"  "CP$                                            'add char count
2210 DIM T(LP+1)
2250 GOTO 840
9250 '
9260 LOCATE 23,1:PRINT"Do you wish to <P*>rint this for the Cm, do <A>nother, or <Q>uit? ";:LINE INPUT Q$
9270 ON INSTR("PpAaQq",Q$)GOTO 9280,9280,10,10,9500,9500:GOTO 9260
9280 ON ERROR GOTO 18000
9290 WIDTH "lpt1:",255    'needed by GWBASIC
9300 EM(0)=-12971:EM(1)=23813:EM(2)=-28469
9310 X=VARPTR(EM(0))
9320 CALL X
9340 FOR N=1 TO 20:LPRINT:NEXT   'space to bottom of page
9350 LPRINT TI$ " encipherment by Mike Barlow   "DATE$:LPRINT
9360 LPRINT OP$
9370 ON ERROR GOTO 0
9380 LPRINT CHR$(12):GOTO 9260
9500 LOCATE 23,1:PRINT SPC(79):LOCATE 23,39:PRINT "end"
9510 LOCATE 23,1:END
10000 '
                    ----  examples ----
10010 XPT$="ACCORDING TO ELCY THE VIGENERE CIPHER WAS FIRST USED IN FIFTEEN HUNDRED AND EIGHTY SIX":XSO$="ACA&U P53":XKW$="PERIODIC" :TI$="VIGENERE":RETURN 'vig
10020 XPT$="C EQUALS K MINUS P":XSO$="ACA&U P26":XKW$="RECIPROCAL":TI$="BEAUFORT":RETURN
10030 XPT$="C EQUALS P MINUS K":XSO$="ACA&U P53":XKW$="APPLE":TI$="VARIANT":RETURN
10040 XPT$="THIS ONE USES TEN OF THE TWENTYSIX VIGENERE ALPHABETS":XSO$="ACA&U P35":XKW$="9321492":TI$="GRONSFELD":RETURN
10050 XPT$="ENCIPHERMENT IS RECIPROCAL":XSO$="ACA&U P43":TI$="PORTA ":XKW$="PORTA":RETURN
18000 '   --- printer error trap ---
18010 IF ERR=24 OR ERR=25 OR ERR=27 THEN LOCATE 23,1:PRINT SPC(79):LOCATE 23,15: PRINT "Printer not ready; press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79):RESUME
18020 ON ERROR GOTO 0:STOP
60000 ' revised Oct 6 1989 to include standard Cm printout and various other cosmetics.
65000 SAVE"allvigs.enc",A
