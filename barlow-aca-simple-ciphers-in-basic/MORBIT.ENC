10 '*** MORBIT.ENC ***  MB Mar 90
20 KEY OFF:CLEAR:DEFINT A-Z:DIM MC(5,26),M(2000)
30 RANDOMIZE TIMER
40 GOSUB 15000                 'load Morse matrix
50 GOSUB 15150                 'set up Morse sounds
60 GOSUB 20000                 'pick up default example
70 GOTO 9500
80 :
90 ' --- TRANSLATE TO INTERMEDIATE TEXT-MORSE CODE ---
100 P=0:PRINT "Sample Morse code: ":COLOR 0,7
110 FOR N=1 TO NR                          'NR is # of PT chars incl spaces
120  X$=MID$(P$,N,1)                       'get a plain character
130  T=INSTR(AL$,X$)                       'get it's posn in alphabet (sp=0)
140  K=0                                   'these variables are Caxton Foster's
150  K=K+1
160  P=P+1
170  M(P)=MC(K,T):IF N>=15 GOTO 180        'limit to 15 chars sounded
171  IF M(P)=1 THEN SOUND FREQ,DIT!:SOUND SIL,DIT!:PRINT ".";:GOTO 180
172  IF M(P)=2 THEN SOUND FREQ,DAH!:SOUND SIL,DIT!:PRINT "-";:GOTO 180
173  IF M(P)=3 THEN SOUND SIL, 2*DIT!:PRINT " ";
180  IF M(P)<>3 GOTO 150
190 NEXT:COLOR 7,0
200 P=P+1                                  'add final spacer
210 M(P)=3                                 'make it a 3
220 IF P/2<>INT(P/2) THEN P=P+1:M(P)=3     'text to be even number of symbols
240 :
280 PRINT:PRINT:PRINT "Morbit equivalents are:
290 COLOR 0,7:PRINT " "+KW$
292 PRINT " . . . - - - x x x ":PRINT " . - x . - x . - x ":COLOR 7,0
295 :
300 '   --- Convert intermediate text to ciphertext ---
310 OP$="":PRINT"Ciphertext is:":COLOR 0,7           'reverse video
320 FOR N=1 TO P STEP 2
330   X$=RIGHT$(STR$(M(N)),1)+RIGHT$(STR$(M(N+1)),1) 'convert to 2-digit string
340   X$=X$+" "
350   X=INSTR(TWO$,X$):IF X=0 THEN BEEP:STOP         'locate in table
355  X=-INT(-X/3)
360   OP$=OP$+MID$(KW$,1+(X-1)*2,1)                  'get corresp key digit
370   M=M+1:IF M MOD 5=0 THEN OP$=OP$+" "            'put in fives
380 NEXT
390 PRINT OP$
400 COLOR 7,0                                        'restore video to normal
410 GOTO 9260
1990 :
2000 '   --- SUB: generate nine non-repeating digits ---
2010 :
2020 FOR N=1 TO 9
2030  X=INT(RND*9+1)
2040  X$=RIGHT$(STR$(X),1)
2050  IF INSTR(KW$,X$) THEN N=N-1:GOTO 2070
2060  KW$=KW$+X$+" "
2070 NEXT
2080 RETURN
4990 :
5000 ' --- misc subs ---
5010 CLS:PRINT DATE$ TAB(29)"* MORBIT ENCIPHERMENT *" TAB(65)"by MIKE BARLOW":PRINT TAB(31)STRING$(19,"-")
5015 RETURN
5020 LOCATE 23,1:PRINT SPC(79);:LOCATE 23,28:PRINT "Press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79);:RETURN
5100 LOCATE 23,1:PRINT SPC(79):LOCATE 23,39:PRINT "end":LOCATE 23,1:END
9240 :
9250 ' --- ENCPRT.STD ---
9260 LOCATE 23,1:PRINT"Do you wish to <P*>rint this for the Cm, do <A>nother, or <Q>uit? ";:LINE INPUT Q$
9270 ON INSTR("PpAaQq",Q$)GOTO 9280,9280,10,10,5100,5100:GOTO 9260
9280 ON ERROR GOTO 18000
9290 WIDTH "lpt1:",255           'needed by GWBASIC
9300 EM(0)=-12971:EM(1)=23813:EM(2)=-28469
9310 X=VARPTR(EM(0))
9320 CALL X
9340 FOR N=1 TO 20:LPRINT:NEXT   'space to bottom of page
9350 LPRINT "Pollux encipherment by Mike Barlow   "DATE$:LPRINT
9360 FOR N=1 TO P:LPRINT USING "#";OP;: IF N MOD 5=0 THEN LPRINT " ";
9365 NEXT
9370 ON ERROR GOTO 0
9380 LPRINT CHR$(12):GOTO 9260
9490 :
9500 '   ---   start here   ---
9510 GOSUB 5010
9520 TWO$="11 12 13 21 22 23 31 32 33 "   'morbit table dot=1,dash=2,sp=3
9530 AL$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
9540 LOCATE 7,1:PRINT "The Morbit cipher requires a nine-digit non-repeating key.":PRINT
9550 PRINT:PRINT"Press Enter for Automatic key generation, X for default example: ";:Y=CSRLIN:X=POS(0)
9560 LINE INPUT Q$:IF Q$="X" OR Q$="x" THEN KW$=XKW$:LOCATE Y,X:PRINT "X":GOTO 9590
9570 :
9580 GOSUB 2000
9590 PRINT:PRINT"Key number is: "KW$:GOSUB 5020:GOSUB 5010
9600 '   ---   enter data   ---
9620 LOCATE 4,1:PRINT"ENTER THE PLAIN TEXT (either case, maintain spaces, 50-75 chars max):
9630 Y=CSRLIN:LINE INPUT PT$: IF PT$="" THEN PT$=XPT$:LOCATE Y,1:PRINT PT$
9640 NC=LEN(PT$):OP$=""
9650 FOR N=1 TO NC
9660   X$=MID$(PT$,N,1)
9670   IF X$=" " THEN Y$=X$:GOTO 9700              'retain spaces
9680   IF X$<"A" THEN GOTO 9710                    'remove punctuation
9690   Y$=CHR$(ASC(X$) AND 223)                    'force UC
9700   OP$=OP$+Y$
9710 NEXT
9720 P$=OP$:NR=LEN(P$)
9730 PRINT:PRINT"Including spaces, there are"NR"characters in the revised text: ":PRINT P$:Y1=CSRLIN
9790 GOSUB 5020:LOCATE Y1+1,1:GOTO 100
9798 :
15000 '   --- READ IN MORSE MATRIX DOT=1, DASH=2, LETTER SPACE=3 ---
15010 FOR I=0 TO 26
15020   K=1
15030   READ X
15040   MC(K,I)=X
15050   K=K+1
15060   IF X<>3 GOTO 15030
15070 NEXT I:RETURN
15080 '
15090 DATA 3,1,2,3,2,1,1,1,3,2,1,2,1,3,2,1,1,3,1,3,1,1,2,1,3
15100 DATA 2,2,1,3,1,1,1,1,3,1,1,3,1,2,2,2,3,2,1,2,3,1,2,1,1,3
15110 DATA 2,2,3,2,1,3,2,2,2,3,1,2,2,1,3,2,2,1,2,3,1,2,1,3
15120 DATA 1,1,1,3,2,3,1,1,2,3,1,1,1,2,3,1,2,2,3,2,1,1,2,3
15130 DATA 2,1,2,2,3,2,2,1,1,3
15140 '
15150 ' --- set the morse sounds ---
15160 FREQ=600:WPM=24:SIL=32767 'freq,wpm,special silence code
15170 DIT!=21.84/WPM:DAH!=3*DIT!
15180 RETURN
17990 :
18000 '   --- printer error trap ---
18010 IF ERR=24 OR ERR=25 OR ERR=27 THEN LOCATE 23,1:PRINT SPC(79):LOCATE 23,15: PRINT "Printer not ready; press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79):RESUME
18020 ON ERROR GOTO 0:STOP
19990 :
20000 '                    ----  examples ----
20010 XPT$="Once upon a time. "                             'ACA&U p38
20011 XPT$=XPT$+XPT$+XPT$
20012 XPT$=XPT$+XPT$+XPT$
20013 XPT$=LEFT$(XPT$,79)             'use 79 for max  58 for min test
20040 XKW$="9 5 8 4 2 7 1 3 6 "    'WISECRACK
20100 RETURN
65000 SAVE"morbit.enc",A
