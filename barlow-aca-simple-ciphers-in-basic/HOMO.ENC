10 '*** HOMO.ENC *** Homophonic encipherer MB Feb 90
20 KEY OFF:CLEAR:DEFINT A-Z:DIM ROW$(4)
30 RANDOMIZE TIMER
40 GOTO 9500
100 ' --- now for the Homophonic bit ---
110 LOCATE 17,1:PRINT "Enter a four-letter keyword, e.g GOLF: ";:LINE INPUT KW$
120 IF LEN(KW$)=0 THEN KW$=XKW$:LOCATE 17,41:PRINT KW$
130 IF LEN(KW$)<>4 THEN PRINT"Wrong length. Try again.":GOSUB 5020:GOTO 110
140 GOSUB 5020
150 GOSUB 5010
160 LOCATE 1,65: PRINT "Keyword: "KW$
170 LOCATE 4,1:COLOR 0,7:PRINT "Plaintext is: ":COLOR 7,0:PRINT PT$
180 PRINT:COLOR 0,7:PRINT" Equivalents: ":COLOR 7,0:PRINT SAL25$
190 :
200 '   --- adjust alpha equivalents according to keyword ---
210 FOR N=1 TO 4                       'for each row
220  X$=MID$(KW$,N,1)
230  P=INSTR(AL25$,X$)
240  ROW$(N)=RIGHT$(ROW$(N),3*(P-1))+LEFT$(ROW$(N),3*(26-P))
250  PRINT ROW$(N)
260 NEXT
300 '   --- now get equivs of each PT letter ---
310 FOR N=1 TO LEN(P$)
320  X$=MID$(P$,N,1)
330  IF X$<"A" THEN CT$=CT$+"   ":GOTO 370
340  P=INSTR(AL25$,X$)
350  J=INT(RND*4+1)
360  CT$=CT$+MID$(ROW$(J),1+3*(P-1),3)
370 NEXT
400 '   --- and print the ciphertext ---
410 PRINT:COLOR 0,7:PRINT " Ciphertext is: ":COLOR 7,0
420 PRINT CT$
490 GOTO 9260
4990 :
4999 STOP
5000 ' --- misc subs ---
5010 CLS:PRINT DATE$ TAB(27)"* HOMOPHONIC ENCIPHERMENT *" :PRINT TAB(29)STRING$(23,"-"):RETURN
5020 LOCATE 23,1:PRINT SPC(79);:LOCATE 23,28:PRINT "Press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79);:RETURN
5100 LOCATE 23,1:PRINT SPC(79):LOCATE 23,39:PRINT "end":LOCATE 23,1:END
9250 ' --- ENCPRT.STD ---
9260 LOCATE 23,1:PRINT"Do you wish to <P*>rint this for the Cm, do <A>nother, or <Q>uit? ";:LINE INPUT Q$
9270 ON INSTR("PpAaQq",Q$)GOTO 9280,9280,10,10,5100,5100:GOTO 9260
9280 ON ERROR GOTO 18000
9290 WIDTH "lpt1:",255           'needed by GWBASIC
9300 EM(0)=-12971:EM(1)=23813:EM(2)=-28469
9310 X=VARPTR(EM(0))
9320 CALL X
9340 FOR N=1 TO 20:LPRINT:NEXT   'space to bottom of page
9350 LPRINT "Homophonic encipherment by Mike Barlow   "DATE$:LPRINT
9360 LPRINT CT$
9370 ON ERROR GOTO 0
9380 LPRINT CHR$(12):GOTO 9260
9490 :
9500 '   ---   start here   ---
9510 GOSUB 5010
9520 PRINT:PRINT TAB(33)"By Mike Barlow"
9530 AL25$="ABCDEFGHIKLMNOPQRSTUVWXYZ"   'no J in alphabet
9535 SAL25$="A  B  C  D  E  F  G  H  IJ K  L  M  N  O  P  Q  R  S  T  U  V  W  X  Y  Z"
9540 '   ---   enter data   ---
9550 FOR M=1 TO 4
9560   FOR N=1 TO 25
9570     READ X$: ROW$(M)=ROW$(M)+X$+" "
9580   NEXT
9590 NEXT
9600 GOSUB 20000  'pick up default example XPT$
9610 :
9620 LOCATE 7,1:PRINT"ENTER THE PLAIN TEXT (either case, with or w/o spaces, ACA limits 50-75 letters max):"
9630 Y=CSRLIN:LINE INPUT PT$: IF PT$="" THEN PT$=XPT$:LOCATE Y,1:PRINT PT$
9640 :
9650 ' --- change PT$ to unspaced U.C P$ ---
9660 NC=LEN(PT$):FOR N=1 TO NC:IF MID$(PT$,N,1)=" " THEN 9670 ELSE P$=P$+CHR$(ASC(MID$(PT$,N,1)) AND 223)  'remove spaces and force UC
9670 NEXT:NR=LEN(P$)
9680 PRINT:PRINT"With spaces removed, there are"NR"characters in the text."
9690 :
9700 PRINT:PRINT"Do you wish to retain word divisions? (Y/N*): ";:Y=CSRLIN:X=POS(0):LINE INPUT Q$:LOCATE Y,X
9710  IF Q$="Y" OR Q$="y" THEN PRINT "Y" ELSE PRINT "N":GOTO 9810
9720 :
9730 '   --- change PT$ to spaced U.C P$ ---
9740 P$="":FOR N=1 TO NC: P$=P$+CHR$(ASC(MID$(PT$,N,1)) AND 223) 'force UC
9750 NEXT
9790 :
9800 '   --- make any J's into I's ---
9810 FOR N=1 TO LEN(P$)
9820  IF MID$(P$,N,1)="J" THEN MID$(P$,N,1)="I"
9830 NEXT
9840 GOTO 100
18000 '   --- printer error trap ---
18010 IF ERR=24 OR ERR=25 OR ERR=27 THEN LOCATE 23,1:PRINT SPC(79):LOCATE 23,15: PRINT "Printer not ready; press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79):RESUME
18020 ON ERROR GOTO 0:STOP
20000 '                    ----  examples ----
20010 XPT$="Word divisions may be kept": XKW$="GOLF" 'ACA&U p35
20100 RETURN
30000 DATA 01,02,03,04,05,06,07,08,09,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25
30010 DATA 26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50
30020 DATA 51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75
30030 DATA 76,77,78,79,80,81,82,83,84,85,86,87,88,89,90,91,92,93,94,95,96,97,98,99,00
65000 SAVE"homo.enc",A
