10 '** SQUARE/ENC **   BY MIKE BARLOW Sep 89
20 'Program to encipher five ciphers which use Polybius Square techniques.
30 CLS:CLEAR 2500:DEFINT A-Z
40 DIM P(10,25)                     'P() is in route choice
50 AL$="ABCDEFGHIKLMNOPQRSTUVWXYZ" '25 letter alphabet (no J)
60 DEF FNMO(A,B)=A-B*INT(A/B)       'A MOD B function
65 TYPE$(1)="Playfair":TYPE$(2)="Two-square":TYPE$(3)="Tri-square":TYPE$(4)="Four-square":TYPE$(5)="Phillips"
70 GOTO 8000
80 '
100 '   --- SUB: Playfair enciphering routine ---
102 '
  do a rectangle
104 P1=INSTR(KA$,T1$)-1:P2=INSTR(KA$,T2$)-1 'find posn in KA$
106 X1=P1-INT(P1/5)*5                'X1 is horiz posn of P1
108 X2=P2-INT(P2/5)*5                'X2 is horiz posn of P2
110 HD=X2-X1                         'HD is horiz diff between P2 and P1
112 IF INT(P1/5)=INT(P2/5)THEN 120     '= same row
114 IF HD=0 THEN 130                    '= Same Column
116 C1=P1+HD:C2=P2-HD                'add diffs to origs
118 GOTO 144
120 '
  do a row
122 C1=P1+1:C2=P2+1                  'move right one posn
124 IF X1=4 THEN C1=C1-5                'wrap round on same row
126 IF X2=4 THEN C2=C2-5
128 GOTO 144
130 '
  do a column
132 Y1=INT(P1/5)                     'get Y posn of chars
134 Y2=INT(P2/5)
136 C1=P1+5:C2=P2+5                  'move down one row
138 IF Y1=4 THEN C1=C1-5*5              'wrap round on same col
140 IF Y2=4 THEN C2=C2-5*5 
142 '
  print pairs of characters
144 CT$(N)=MID$(KA$,C1+1,1)+MID$(KA$,C2+1,1):PRINT CT$(N)" ";
146 OP$=OP$+CT$(N)+" "
148 RETURN
158 '
160 '   ---  display Playfair texts  ---
162 LOCATE 11,1:PRINT"Plaintext pairs are:  ";
164 FOR N=1 TO NG
166   PT$(N)=MID$(WT$,(N*2)-1,2):PRINT PT$(N)" ";
168 NEXT:PRINT
170 PRINT:PRINT"Ciphertext pairs are: ";
171 OP$=""
172 FOR N=1 TO NG
174   T1$=LEFT$(PT$(N),1):T2$=RIGHT$(PT$(N),1):GOSUB 104
176 NEXT:PRINT
178 GOTO 9260
195 '
200 '   --- SUB: Two-square enciphering routine ---
205 OP$="":FOR N=1 TO LEN(WT$)STEP 2
210   P1$=MID$(WT$,N,1):P2$=MID$(WT$,N+1,1)
215   Z1=INSTR(JW$,P1$)     'get posn of 1st pt letter in alph1
220   Z2=INSTR(KW$,P2$)     'get posn of 2nd pt letter in alph2
225   X1=FNMO(Z1,5):Y1=-INT(-Z1/5) 'get co-ords in sq 1
230   IF X1=0 THEN X1=5
235   X2=FNMO(Z2,5):Y2=-INT(-Z2/5) 'get co-ords in sq 2
240   IF X2=0 THEN X2=5
245   X1$=MID$(JW$,X1+(Y2-1)*5,1)  'transfer to sq 1
250   X2$=MID$(KW$,X2+(Y1-1)*5,1)  'transfer to sq 2
255   OP$=OP$+X2$+X1$+" "
260 NEXT
265 PRINT:PRINT:PRINT"Plain text is: "PT$
270 PRINT:PRINT"Keywords are "K1$" and "K2$
280 PRINT:PRINT"Enciphered text is: "OP$
285 RETURN
300 '   --- SUB: Tri-sq enciphering routine ---
305 OP$="":FOR N=1 TO LEN(WT$)STEP 2
310   P1$=MID$(WT$,N,1):P2$=MID$(WT$,N+1,1)
315   Z1=INSTR(A1$,P1$)     'get posn of 1st pt letter in alph1
320   Z2=INSTR(A2$,P2$)     'get posn of 2nd pt letter in alph2
325   X1=FNMO(Z1,5):Y1=-INT(-Z1/5) 'get co-ords in sq1
330   IF X1=0 THEN X1=5
335   X2=FNMO(Z2,5):Y2=-INT(-Z2/5) 'get co-ords in sq2
340   IF X2=0 THEN X2=5
345   X3$=MID$(A3$,X2+(Y1-1)*5,1)  'transfer to sq3
350   X4$=MID$(A1$(X1),INT(1+RND*(5)),1)
355   X5$=MID$(A2$(Y2),INT(1+RND*(5)),1)
360   OP$=OP$+X4$+X3$+X5$+" "
365 NEXT
370 C=4:D=5:E=29:F=50:XX$=PT$:GOSUB 5010
375 LOCATE D-1,E:PRINT"Plain text is: "
380 LOCATE D+C+1,E:PRINT"Keywords are "K1$" and "K2$" and "K3$
385 LOCATE 17,1:PRINT"Enciphered text is: "OP$
390 RETURN
400 '   --- SUB: four-sq enciphering routine ---
405 OP$=""
410 FOR N=1 TO LEN(WT$)STEP 2
415   P1$=MID$(WT$,N,1):P2$=MID$(WT$,N+1,1)
420   Z1=INSTR(AL$,P1$)      'get posn of 1st pt letter in alph
425   Z2=INSTR(AL$,P2$)      'get posn of 2nd pt letter in alph
430   X1=FNMO(Z1,5):Y1=-INT(-Z1/5) 'get co-ords in sq 1
435   IF X1=0 THEN X1=5
440   X3=FNMO(Z2,5):Y3=-INT(-Z2/5) 'get co-ords in sq 3
445   IF X3=0 THEN X3=5
450   X2$=MID$(JW$,X3+(Y1-1)*5,1)  'transfer to sq 2
455   X4$=MID$(KW$,X1+(Y3-1)*5,1)  'transfer to sq 4
460   OP$=OP$+X2$+X4$+" "
465 NEXT
470 C=4:D=5:E=29:F=50:XX$=PT$:GOSUB 5010
475 LOCATE D-1,E:PRINT"Plain text is: "
480 LOCATE D+C+1,E:PRINT"Keywords are "K1$" and "K2$
485 LOCATE 17,1:PRINT"Enciphered text is: "OP$
490 RETURN
500 '   --- SUB: Phillips enciphering routine ---
505 M=1:K=1:OP$=""
510 FOR N=1 TO LEN(WT$)
515   P$=MID$(WT$,N,1)
520   X=INSTR(A$(M),P$)      'get posn of 1st pt letter in alph
525   IF X/5=INT(X/5) THEN X=X+1 ELSE X=X+6 'change by rules
530   X$=MID$(A$(M),X,1)                    'get subst char
535   OP$=OP$+X$
540   IF N/5=INT(N/5)THEN M=M+1:IF M=9 THEN M=1
545   K=K+1:IF K=6 THEN K=1:OP$=OP$+" "    'space the ciphertext
550 NEXT
555 C=4:D=5:E=29:F=50:XX$=PT$:GOSUB 5010
560 LOCATE D-1,E:PRINT"Plain text is: "
565 LOCATE D+C+1,E:PRINT"Keyword is "K1$
570 LOCATE 17,1:PRINT"Enciphered text is: "OP$
575 RETURN
700 '
710 ' --- SUB: check for l.case, J's and duplicates in KW$ ---
720 OP$="":FOR N=1 TO LEN(KW$) 
730  X$=CHR$(ASC(MID$(KW$,N,1))AND 223):IF X$="J"THEN X$="I"
740  IF INSTR(OP$,X$)THEN X$=""
750  OP$=OP$+X$
760 NEXT:KW$=OP$:OP$=""
770 RETURN
780 '
790 '  --- SUB: make up keyword alphabet KA$ from KW$ ---
800 KA$=AL$
810 FOR N=1 TO LEN(KW$)
820   X=INSTR(KA$,MID$(KW$,N,1))           'find posn of KW letter in alphabet
830   X$=LEFT$(KA$,X-1)                    'take the alphabet to the left of it
840   KA$=X$+RIGHT$(KA$,LEN(KA$)-X)        'and add to the remainder
850 NEXT
860 KA$=KW$+KA$                            'now put the KW in front
880 '
890 '   --- select route for square construction ---
900 PRINT"Which route to make the square?
910 A=0:B=32
920 PRINT TAB(1+A)"(1) Regular horizontals"TAB(1+B)"(6) Alternate Diagonals"
930 PRINT TAB(1+A)"(2) Alternate horizontals"TAB(1+B)"(7) Clockwise spiral"
940 PRINT TAB(1+A)"(3) Regular verticals"TAB(1+B)"(8) Anticlockw. spiral
950 PRINT TAB(1+A)"(4) Alternate verticals"TAB(1+B)"(9) Clockwise inside-out"
960 PRINT TAB(1+A)"(5) Regular diagonals"TAB(1+B)"(0*) Clockw from bottom LH corner
980 PRINT"Choose: ";:INPUT RN:IF RN=0 THEN RN=10
985 IF RN<0 OR RN>10 THEN 980
990 '
1000 '   --- put KA$ in route sequence ---
1010 OP$="":FOR N=1 TO 25
1020  X$=MID$(KA$,P(RN,N),1)
1030  OP$=OP$+X$
1040 NEXT
1050 KA$=OP$
1060 RETURN
1080 '
1090 '   --- print the squares (OPTIONAL) ---
1100 '   --- SUB: print one square ---
1110 CLS:GOSUB 4010:A=37
1117 LOCATE 1,65:PRINT "By Mike Barlow"
1120 FOR N=0 TO 4:LOCATE N+5,A:PRINT MID$(KA$,1+5*N,5);:NEXT:PRINT
1130 RETURN
1190 '   --- SUB: print two squares ---
1200 CLS:GOSUB 4020:A=35:B=45:PRINT:PRINT TAB(A)" (1)       (2)
1210 FOR N=0 TO 4:LOCATE N+5,A:PRINT MID$(JW$,1+5*N,5);:NEXT
1220 FOR N=0 TO 4:LOCATE N+5,B:PRINT MID$(KW$,1+5*N,5);:NEXT
1230 RETURN
1300 '   --- SUB print three squares ---
1310 CLS:GOSUB 4030:A=1:B=8:LOCATE 4,B+1:PRINT "(2)
1320 FOR N=0 TO 4:LOCATE N+5,B:PRINT MID$(A2$,1+5*N,5);:NEXT
1330 PRINT:PRINT" (1)    (3)
1340 FOR N=0 TO 4:LOCATE N+11,A:PRINT MID$(A1$,1+5*N,5);:NEXT
1350 FOR N=0 TO 4:LOCATE N+11,B:PRINT MID$(A3$,1+5*N,5);:NEXT
1360 RETURN
1400 '   --- SUB: print four squares ---
1410 CLS:GOSUB 4040:A=1:B=8:PRINT
1430 FOR N=0 TO 4:LOCATE N+5,A:PRINT MID$(AL$,1+5*N,5);:NEXT
1440 PRINT:PRINT"----- -----"
1450 FOR N=0 TO 4:LOCATE N+11,B:PRINT MID$(AL$,1+5*N,5);:NEXT
1460 FOR N=0 TO 4:LOCATE N+5,B:PRINT MID$(JW$,1+5*N,5);:NEXT
1470 FOR N=0 TO 4:LOCATE N+11,A:PRINT MID$(KW$,1+5*N,5);:NEXT
1480 RETURN
1500 '   --- SUB: print eight squares ---
1505 CLS:GOSUB 4050:A=1:B=7:PRINT:PRINT " (1)    (2)    (3)    (4)
1510 FOR N=0 TO 4:LOCATE N+5,A:PRINT MID$(A$(1),1+5*N,5);:NEXT
1515 FOR N=0 TO 4:LOCATE N+5,A+B:PRINT MID$(A$(2),1+5*N,5);:NEXT
1520 FOR N=0 TO 4:LOCATE N+5,A+2*B:PRINT MID$(A$(3),1+5*N,5);:NEXT
1525 FOR N=0 TO 4:LOCATE N+5,A+3*B:PRINT MID$(A$(4),1+5*N,5);:NEXT
1530 PRINT:PRINT" (5)    (6)    (7)    (8)
1535 FOR N=0 TO 4:LOCATE N+11,A:PRINT MID$(A$(5),1+5*N,5);:NEXT
1540 FOR N=0 TO 4:LOCATE N+11,B:PRINT MID$(A$(6),1+5*N,5);:NEXT
1545 FOR N=0 TO 4:LOCATE N+11,A+2*B:PRINT MID$(A$(7),1+5*N,5);:NEXT
1550 FOR N=0 TO 4:LOCATE N+11,A+3*B:PRINT MID$(A$(8),1+5*N,5);:NEXT
1555 RETURN
3990 '
4000 '   --- titles ---
4010 PRINT DATE$;TAB(28)"LITTLE GEM PLAYFAIR ENCODER":PRINT TAB(28)STRING$(27,"-"):RETURN
4020 PRINT DATE$; TAB(28)"LITTLE GEM TWOSQUARE ENCODER":PRINT TAB(28)STRING$(28,"-"):RETURN
4030 PRINT DATE$; TAB(28)"LITTLE GEM TRISQUARE ENCODER":PRINT TAB(28)STRING$(28,"-"):RETURN
4040 PRINT DATE$; TAB(28)"LITTLE GEM FOURSQUARE ENCODER":PRINT TAB(28)STRING$(29,"-"):RETURN
4050 PRINT DATE$; TAB(28)"LITTLE GEM PHILLIPS ENCODER":PRINT TAB(28)STRING$(27,"-"):RETURN
5000 '   --- SUB: to print C lines of text at (D,E) of length F ---
5010 FOR N=1 TO C
5020   LOCATE D+N-1,E
5030   PRINT MID$(XX$,1+(N-1)*F,F)
5040 NEXT:RETURN
7990 '
8000 '   --- main menu ---
8010 PRINT TAB(28)"POLYBIUS SQUARE ENCIPHERMENT":PRINT TAB(28)STRING$(28,"-"):PRINT:PRINT TAB(34)"By Mike Barlow"
8015 GOSUB 30000     'get route data
8020 PRINT:PRINT:PRINT TAB(7)"Do you wish to encipher a (1) PLAYFAIR (One-square)
8030 A=32:PRINT TAB(1+A)"(2) TWO-SQUARE
8040 PRINT TAB(1+A)"(3) TRI-SQUARE
8050 PRINT TAB(1+A)"(4) FOUR-SQUARE
8060 PRINT TAB(1+A)"(5) PHILLIPS (Eight-square)
8070 PRINT:PRINT TAB(1+A)"Choose: ";:INPUT NS
8080 ON NS GOSUB 20100,20200,20300,20400,20500 'get default example
8081 'XT$=XT$+XT$+XT$+XT$
8090 CLS:ON NS GOSUB 4010,4020,4030,4040,4050    'title
8100 PRINT:PRINT TAB(34)"By Mike Barlow"
8400 IF NS>1 THEN GOTO 9000
8490 '
8500 '   --- enter the Playfair text ---
8520 LOCATE 11,27:PRINT"<-max";:LOCATE 7,1:PRINT"ENTER THE PLAINTEXT (either case, with or without spaces, no punctuation, 186   chars max): ":LOCATE 9,1:PRINT">";:LINE INPUT Q$:IF Q$=""THEN Q$=XT$:LOCATE 9,1:PRINT Q$
8522 PT$=Q$
8525 FOR N=1 TO LEN(Q$):IF MID$(Q$,N,1)=" "THEN 8527
8526   OP$=OP$+MID$(Q$,N,1)
8527 NEXT
8530 Q$=OP$:OP$="":RT$=Q$:WT$="":N1$="X":N2$="Z"              'remembered text, working text, nulls
8540 FOR N=1 TO LEN(Q$)
8550   X$=CHR$(ASC(MID$(Q$,N,1))AND 223):IF X$="J"THEN X$="I"       'change J to I, force u.c
8560   IF RX$="X"THEN N$=N2$ELSE N$=N1$            'choose a null
8570   IF X$=RX$ AND N/2=INT(N/2) THEN X$=N$+X$ 'and insert it if double letters in a pair
8580   RX$=RIGHT$(X$,1)                         'remember the last letter
8590   WT$=WT$+X$                               'assemble the new text
8600 NEXT
8610   IF MID$(WT$,LEN(WT$)-1,1)="X"THEN N$=N2$ELSE N$=N1$  'choose a null
8620 IF LEN(WT$)/2<>INT(LEN(WT$)/2)THEN WT$=WT$+N$         'add a null if odd number of letters
8630 CLS:GOSUB 4010:PRINT"Plaintext is: "LEFT$(RT$,36);:LOCATE 2,11:PRINT MID$(RT$,37,50);:LOCATE 3,11:PRINT MID$(RT$,87,50);:LOCATE 4,11:PRINT MID$(RT$,137,50);
8640 NG=LEN(WT$)/2:DIM CT$(NG),PT$(NG)        'number of groups
8650 GOSUB 11000                              'get the keyword
8660 GOSUB 1100                               'print the square
8670 GOTO 160                                 'print answers
9000 '
9010 '  --- enter the text ---
9029 'XT$=STRING$(186,"A")
9030 LOCATE 11,27:PRINT"<-max";:LOCATE 7,1:PRINT"ENTER THE PLAINTEXT (either case, with or without spaces, no punctuation, 186   chars max): ":LOCATE 9,1:PRINT">";:LINE INPUT Q$:IF Q$=""THEN Q$=XT$:LOCATE 9,1:PRINT Q$
9032 PT$=Q$
9040 FOR N=1 TO LEN(Q$):IF MID$(Q$,N,1)=" "THEN 9044
9042   OP$=OP$+MID$(Q$,N,1)
9044 NEXT
9050 Q$=OP$:OP$="":RT$=Q$:WT$="":              'remembered text, working text, nulls
9060 FOR N=1 TO LEN(Q$)
9070   X$=CHR$(ASC(MID$(Q$,N,1))AND 223):IF X$="J"THEN X$="I"       'change J to I, force u.c
9080   WT$=WT$+X$                        'assemble the new text
9090 NEXT
9092 LW=LEN(WT$)
9094 ON NS GOTO 9100,9100,9100,9100,9096
9096 IF LW/5<>INT(LW/5)THEN WT$=WT$+"THXYZ":WT$=LEFT$(WT$,INT(LW/5)*5+5):GOTO 9105        'add nulls if length not multiple of five
9100 IF LEN(WT$)/2<>INT(LEN(WT$)/2)THEN WT$=WT$+"X"         'add a null if odd number of letters
9105 CLS:ON NS GOSUB 4010,4020,4030,4040,4050    'title
9107 LOCATE 1,65:PRINT "By Mike Barlow"
9110 PRINT:PRINT"Working text is: "WT$
9120 '
9130 ON NS GOSUB 11000,12000,13000,12000,15000 'get keywords
9230 ON NS GOSUB 1100,1200,1300,1400,1500      'print squares 
9235 LOCATE 1,65:PRINT "By Mike Barlow"
9240 ON NS GOSUB 100,200,300,400,500           'encipher
9250 '
9260 LOCATE 23,1:PRINT"Do you wish to <P*>rint this for the Cm, do <A>nother, or <Q>uit? ";:LINE INPUT Q$
9270 ON INSTR("PpAaQq",Q$)GOTO 9280,9280,10,10,9500,9500:GOTO 9260
9280 ON ERROR GOTO 18000
9290 WIDTH "lpt1:",255    'needed by GWBASIC
9300 EM(0)=-12971:EM(1)=23813:EM(2)=-28469
9310 X=VARPTR(EM(0))
9320 CALL X
9340 FOR N=1 TO 20:LPRINT:NEXT   'space to bottom of page
9350 LPRINT TYPE$(NS) " encipherment by Mike Barlow   "DATE$:LPRINT
9360 LPRINT OP$
9370 ON ERROR GOTO 0
9380 LPRINT CHR$(12):GOTO 9260
9500 LOCATE 23,1:PRINT SPC(79):LOCATE 23,39:PRINT "end"
9510 LOCATE 23,1:END
11000 '   --- SUB: enter the Playfair keyword ---
11010 PRINT:PRINT"ENTER THE KEYWORD (either case, no "CHR$(34)"J"CHR$(34)"): ";:LINE INPUT KW$:IF KW$=""THEN KW$=XK$
11020 GOSUB 710
11025 PRINT"Keyword is:  "KW$+STRING$(32," ")
11030 GOSUB 790       'get key alphabet KA$ in route order
11050 RETURN
11990 '
12000 ' ---  SUB: enter the two-sq or four-sq keywords ---
12010 PRINT"ENTER THE FIRST KEYWORD (either case, no "CHR$(34)"J"CHR$(34)"): ";:LINE INPUT K1$:IF K1$=""THEN K1$=XJ$
12020 KW$=K1$:GOSUB 720:K1$=KW$
12030 PRINT"First Keyword is:  "K1$+STRING$(32," ")
12035 KW$=K1$:GOSUB 790:JW$=KA$   'JW$ is 1st enc alphabet
12040 PRINT"ENTER THE SECOND KEYWORD (either case, no "CHR$(34)"J"CHR$(34)"): ";:LINE INPUT K2$:IF K2$=""THEN K2$=XK$
12050 KW$=K2$:GOSUB 720:K2$=KW$
12060 PRINT"Second Keyword is: "K2$+"              "
12080 KW$=K2$:GOSUB 790:KW$=KA$   'kw$ is 2nd enc alphabet
12090 CLS:IF NS=2 THEN GOSUB 4020:GOTO 12100 ELSE GOSUB 4040
12100 RETURN
13000 ' ---  SUB enter the tri-sq keywords ---
13010 PRINT"ENTER THE FIRST KEYWORD (either case, no "CHR$(34)"J"CHR$(34)") : ";:LINE INPUT K1$:IF K1$=""THEN K1$=XI$
13020 KW$=K1$:GOSUB 710:K1$=KW$
13030 PRINT"First Keyword is: "K1$+STRING$(32," ")
13035 KW$=K1$:GOSUB 790:A1$=KA$   'a1$ is 1st enc alphabet
13040 PRINT"ENTER THE SECOND KEYWORD (either case, no "CHR$(34)"J"CHR$(34)"): ";:LINE INPUT K2$:IF K2$=""THEN K2$=XJ$
13050 KW$=K2$:GOSUB 710:K2$=KW$
13060 PRINT"Second Keyword is: "K2$+"              "
13065 KW$=K2$:GOSUB 790:A2$=KA$   'a2$ is 2nd enc alphabet
13070 PRINT"ENTER THE THIRD KEYWORD (either case, no "CHR$(34)"J"CHR$(34)") : ";:LINE INPUT K3$:IF K3$=""THEN K3$=XK$
13080 KW$=K3$:GOSUB 710:K3$=KW$
13090 PRINT"Third Keyword is: "K3$+"              "
13120 KW$=K3$:GOSUB 790:A3$=KA$   'a3$ is 3rd enc alphabet
13160 '
13170 ' -- put A1$ cols into a1$(n) and A2$ rows into a2$(n) --
13180 FOR M=1 TO 5
13190   FOR N=1 TO 5
13200     A1$(N)=A1$(N)+MID$(A1$,N+(M-1)*5,1)
13210   NEXT
13220 NEXT
13230 FOR N=1 TO 5:A2$(N)=MID$(A2$,1+(N-1)*5,5):NEXT
13240 RETURN
15000 ' ---  SUB: enter the Phillips keyword ---
15010 PRINT"ENTER THE KEYWORD (either case, no "CHR$(34)"J"CHR$(34)"): ";:LINE INPUT K1$:IF K1$=""THEN K1$=XK$
15020 KW$=K1$:GOSUB 710:K1$=KW$
15030 PRINT "Keyword is: "K1$+STRING$(32," ")
15040 KW$=K1$:GOSUB 790:A$(1)=KA$   'a1$ is 1st enc alphabet
15050 '
15060 '   --- get the other alphabets ---
15070 FOR N=1 TO 5
15080  A1$(N)=MID$(A$(1),1+(N-1)*5,5)
15090 NEXT
15100 A$(2)=A1$(2)+A1$(1)+A1$(3)+A1$(4)+A1$(5)
15110 A$(3)=A1$(2)+A1$(3)+A1$(1)+A1$(4)+A1$(5)
15120 A$(4)=A1$(2)+A1$(3)+A1$(4)+A1$(1)+A1$(5)
15130 A$(5)=A1$(2)+A1$(3)+A1$(4)+A1$(5)+A1$(1)
15140 A$(6)=A1$(3)+A1$(2)+A1$(4)+A1$(5)+A1$(1)
15150 A$(7)=A1$(3)+A1$(4)+A1$(2)+A1$(5)+A1$(1)
15160 A$(8)=A1$(3)+A1$(4)+A1$(5)+A1$(2)+A1$(1)
15170 FOR N=1 TO 8:A$(N)=A$(N)+A$(N):NEXT 'allow for wraparound
15180 RETURN
18000 '   --- printer error trap ---
18010 IF ERR=24 OR ERR=25 OR ERR=27 THEN LOCATE 23,1:PRINT SPC(79):LOCATE 23,15: PRINT "Printer not ready; press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79):RESUME
18020 ON ERROR GOTO 0:STOP
20000 '   --- Iexamples ---
20100 XT$="COME QUICKLY WE NEED HELP":XK$="LOGARITHM"' ACA&U P41
20110 RETURN
20200 XT$="another digraphic setup":XJ$="DIALOGUE":XK$="BIOGRAPHY"' ACA&U P52 (two-sq)
20210 RETURN
20300 XT$="THREE KEY SQUARES USED":XI$="NOVELS":XJ$="READING":XK$="PASTIME"' ACA&U P52
20310 RETURN
20400 XT$="COME QUICKLY WE NEED HELP":XJ$="GEOMETRY":XK$="LOGARITHM"' ACA&U P31
20410 RETURN
20500 XT$="SQUARES ONE AND FIVE ARE ACTUALLY THE SAME AS ARE SQUARES TWO AND EIGHT":XK$="DIAGONALS":'ACA&U P40
20510 RETURN
30000 '   --- read route data into P(x,y) ---
30010 FOR J=1 TO 10
30020   FOR I=1 TO 25 
30030    READ P(J,I) 
30040  NEXT 
30050 NEXT 
30060 RETURN
30070 REM regular horizontals
30080 DATA 1,2,3,4,5,6,7,8,9,10,11,12,13,14, 15,16,17,18,19,20,21,22,23,24,25 
30090 REM alternate horizontals
30100 DATA 1,2,3,4,5,10,9,8,7,6,11,12,13,14,15,20,19,18,17,16,21,22,23,24, 25
30110 REM regular verticals
30120 DATA 1,6,11,16,21,2,7,12,17,22,3,8,13, 18,23,4,9,14,19,24,5,10,15,20,25
30130 REM alternate verticals 
30140 DATA 1,10,11,20,21,2,9,12,19,22,3,8,13,18,23,4,7,14,17 ,24,5,6,15,16,25 
30150 REM Regular diagonals
30160 DATA 1,2,4,7,11,3,5,8,12,16,6,9,13,17, 20,10,14,18,21,23,15,19,22,24,25 
30170 REM alternate diagonals
30180 DATA 1,3,4,10,11,2,5,9,12,19,6,8,13,18, 20,7,14,17,21,24,15,16,22,23,25 
30190 REM clockwise square with top-left start 
30200 DATA 1,2,3,4,5,16,17,18,19,6,15,24,25, 20,7,14,23,22,21,8,13,12,11,10,9 
30210 REM anticlockwise square with top-left start 
30220 DATA 1,16,15,14,13,2,17,24,23,12,3,18,25,22,11,4,19,20,21,10,5,6,7,8 ,9
30230 REM clockwise inside out
30240 DATA 21,22,23,24,25,20,7,8,9,10,19,6,1,2,11,18,5,4,3,12,17,16,15,14, 13
30250 REM clockwise square with bottom-left start 
30260 DATA 5,6,7,8,9,4,19,20,21,10,3,18,25, 22,11,2,17,24,23,12,1,16,15,14,13 
60000 '9510 adjusted Oct 6 1989
65000 SAVE"SQUARE.ENC",A
