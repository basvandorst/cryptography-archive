10 :REM ** GRILLE ENCODER ** by M.Barlow apr 30 1985 ja8
15 KEY OFF
20 CLS:CLEAR :DEFINT A-Z:PT$=STRING$(150,"#")
25 V!=TIMER:RANDOMIZE V!
30 DEF FN Y$(R,C)=MID$(STR$(R),2)+"x"+MID$(STR$(C),2):REM PRINT FORMATTER
40 CC$=" 1 2 3 4 5 6 7 8 9 0 1 2":REM column headings
50 XPT$="THETURNINGGRILLE": XS=4 :REM DEFAULTS FOR ACA&O0U EXAMPLE
51 XPT$="KINDLY REFRAIN FROM SMOKING WHILST I AM DEMONSTRATING THIS SUPERB PROGRAMX":XS=8
60 :REM        -----> Start here <-----
70 PRINT DATE$ TAB(28)"LITTLE GEM GRILLE ENCODER" TAB(69)"Version JA8":PRINT TAB(28)STRING$(25,"-"):PRINT :PRINT TAB(14)"By Mike Barlow, Bill Sutton and Harry Addicott, Mar 1985":PRINT
80 :REM         -----> Input the text <-----
90 PT$="":LOCATE  6, 1:PRINT  "ENTER THE PLAINTEXT (either case, with or without spaces, but no punctuation): ":LINE INPUT PT$:IF PT$="" THEN PT$=XPT$:LOCATE 7,25:PRINT PT$
92 ZZ$="":RP$=PT$        'remember orig text
94 FOR N=1 TO LEN(PT$)
96   X$=MID$(PT$,N,1):IF X$=" " THEN 98   'remove spaces
97   ZZ$=ZZ$+CHR$(ASC(X$) AND 223)        'force upper case
98 NEXT:PT$=ZZ$
100 LOCATE 10,1:PT=LEN(PT$):PRINT "That is"PT"letters. Do you wish to proceed? (Y*/N) ";:X=POS(0): GOSUB 660:IF Q$="N" OR Q$="n" THEN GOTO 10 ELSE LOCATE 10,X:PRINT"Y"
110 S=0:LOCATE 12,1:PRINT "GRILLE SIDE (range 4 - 12, must be an even number)";:Y=CSRLIN:X=POS(0):INPUT S
111 IF S = 0 THEN S = XS: LOCATE Y,X:PRINT S
120 IF S>12 THEN PRINT "TOO LARGE!": GOTO 110
130 IF S<4 THEN PRINT "TOO SMALL!": GOTO 110
140 IF S/2<>INT(S/2) THEN  PRINT "MUST BE AN EVEN NUMBER!": GOTO 110
150 IF PT=S*S THEN 170: ELSE IF PT<S*S THEN PT$=LEFT$(PT$+STRING$(S*S-PT,"X"),S*S): GOTO 160: ELSE PT$=LEFT$(PT$,S*S):REM PAD WITH X's or cut
160 PRINT :PRINT "The plaintext has been adjusted to fit the number of apertures, and now reads:          ":PRINT PT$: GOSUB 680
170 NH=INT(S*S*.25) :REM NH = number of holes
175 PRINT:PRINT"There will be"NH"holes in the grille,"(NH*4)"ciphertext letters.":GOSUB 680
180 DIM A(S+1,S+1),A$(S+1,S+1)
190  LOCATE 16, 31-S/2:PRINT "Drilling the holes ";
200 :REM         -----> Generate the Grille <-----
210 FOR I=1 TO NH
220  'READ R,C 'activate this line for ACA&U example and de-activate line 230.
230  R=1+INT(RND*S):C=1+INT(RND*S):IF A(R,C) THEN 230    'skip if occupied
240  A(R,C)=1:A(C,S-R+1)=2:A(S-R+1,S-C+1)=3:A(S-C+1,R)=4:REM OCCUPY
250  PRINT ".";
260 NEXT
270 :REM         ----> Set up the field, fill it with dots <----
280 CLS:PRINT DATE$ TAB(28)"LITTLE GEM GRILLE ENCODER" TAB(69)"Version JA8":PRINT TAB(28)STRING$(25,"-"):LOCATE 3,1:COLOR 0,7:PRINT "The plaintext is:":COLOR 7,0:PRINT RP$
285  LOCATE 7,3:PRINT LEFT$(CC$,S*2)
290 FOR N=1 TO S: LOCATE 7+N,1: PRINT N;:NEXT
300 FOR  R=1 TO S:FOR C=1 TO S:LOCATE 7+R,C*2+2: PRINT".";:NEXT :NEXT
310 :REM         ----> Mark the apertures <----
320 FOR  R=1 TO S:FOR  C=1 TO S
330  LOCATE 7+R,C*2+2
340  IF A(R,C)=1 THEN PRINT "*";
350 NEXT :NEXT 
360 :REM        -----> Put a letter in each aperture <-----
380 N=1:L=1
390 FOR  R=1 TO S:FOR  C=1 TO S
400  ON L GOTO 410,420,430,440
410  IF A(R,C)=1 THEN A$(R,C)=MID$(PT$,N,1):N=N+1: GOTO 450: ELSE 450
420  IF A(R,C)=2 THEN A$(R,C)=MID$(PT$,N,1):N=N+1: GOTO 450: ELSE 450
430  IF A(R,C)=3 THEN A$(R,C)=MID$(PT$,N,1):N=N+1: GOTO 450: ELSE 450
440  IF A(R,C)=4 THEN A$(R,C)=MID$(PT$,N,1):N=N+1: GOTO 450: ELSE 450
450 NEXT :NEXT
460 L=L+1:IF L>4 THEN 480: ELSE  GOTO 390
470 :REM       -----> Display the Answer <-----
480 COLOR 0,7:LOCATE 18,1:PRINT"The ciphertext is:":COLOR 7,0:
490 N=1:NN=1:OP$=""
495 FOR  R=1 TO S:FOR  C=1 TO S
500  PRINT A$(R,C);:N=N+1:OP$=OP$+A$(R,C):IF N>5 THEN PRINT " ";:OP$=OP$+" ":N=1:NN=NN+1:IF NN>13 THEN PRINT :NN=1
510 NEXT C:NEXT R
520 LOCATE  8,43:PRINT "The"NH"holes in this "FN Y$(S,S)" grille";:LOCATE  9,43:PRINT "are in positions:";:N=1:NN=0
530 FOR  R=1 TO S:FOR  C=1 TO S:IF A(R,C)=1 THEN LOCATE NN+10,41+(N*4): PRINT USING "### "; S*(R-1)+C;: N=N+1: IF N/8=INT(N/8) THEN NN=NN+1: N=1
540 NEXT :NEXT 
550 GOTO 9260
650 :REM        -----> Misc Subs <-----
660 LOCATE ,,1:Q$= INPUT$(1): RETURN
680 LOCATE 22,27:PRINT "Press <ENTER> to proceed: ";: LINE INPUT Q$: RETURN
690 DATA 1,1,2,4,3,2,3,4::REM R,C posns for ACA&U example
9250 '
9260 LOCATE 23,1:PRINT"Do you wish to <P*>rint this for the Cm, do <A>nother, or <Q>uit? ";:LINE INPUT Q$
9270 ON INSTR("PpAaQq",Q$)GOTO 9280,9280,10,10,9500,9500:GOTO 9260
9280 ON ERROR GOTO 18000
9290 WIDTH "lpt1:",255    'needed by GWBASIC
9300 EM(0)=-12971:EM(1)=23813:EM(2)=-28469
9310 X=VARPTR(EM(0))
9320 CALL X
9340 FOR N=1 TO 20:LPRINT:NEXT   'space to bottom of page
9350 LPRINT "GRILLE encipherment by Mike Barlow   "DATE$:LPRINT
9360 LPRINT OP$
9370 ON ERROR GOTO 0
9380 LPRINT CHR$(12):GOTO 9260
9500 LOCATE 23,1:PRINT SPC(79):LOCATE 23,39:PRINT "end"
9510 LOCATE 23,1:END
18000 '   --- printer error trap ---
18010 IF ERR=24 OR ERR=25 OR ERR=27 THEN LOCATE 23,1:PRINT SPC(79):LOCATE 23,15: PRINT "Printer not ready; press <ENTER> to proceed: ";:LINE INPUT Q$:LOCATE 23,1:PRINT SPC(79):RESUME
18020 ON ERROR GOTO 0:STOP
60000 ' Plaintext display added; Cm printout added; non-default data entry and space elimination corrected Oct 1989
65000 SAVE"grille.enc",A
