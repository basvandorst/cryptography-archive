1000 '
1010 '
1020 '         <<<<<  P C - C O D E 2  >>>>>
1030 '
1040 '
1050 '     * * * * * * * * * * * * * * * * * * * *
1060 '     *                                     *
1070 '     *  COPYRIGHT in Public Domain 1984    *
1080 '     *           by Richard N. Colvard     *
1090 '     *                                     *
1100 '     *  WARNING: This Program must be      *
1110 '     *           Compiled with IBM         *
1120 '     *           BASCOM/T/O                *
1130 '     *           Donot use BASICA          *
1140 '     *                                     *
1150 '     *  Highly recommend 8087 link libs    *
1160 '     *                                     *
1170 '     * * * * * * * * * * * * * * * * * * * *
1180 '
1190 '
1200 DIM B#(11), C#(47), CONS!(7), IX%(256)
1210 CONS!(1)=8: CONS!(2)=131072! : CONS!(3)=8192: CONS!(4)=128
1220 CONS!(5)=2: CONS!(6)=32 : CONS!(7)=2048 : L0% = 95 : K0% = L0% * 2
1230 M%=7: N%=17: YY# = 999991! : MU# = 16807 : MD# = 2147483647#
1240 W1$="123456789012345678901234567890123456789012345678901234567890"
1250 W2$="         1         2         3         4         5         6"
1260 W3$=".........+.........+.........+.........+.........+.........+"
1270 W4$="         7         8         9        10        11        12"
1280 WH$="V1.44 PC-CODE2"
1290 WZ$="P C   C o m p u t e r   S e c u r i t y    " + WH$
1300 HDR$=WH$ + " ENCODED " + DATE$ + " " + TIME$
1310 GOSUB 6240
1320 PRINT " " : PRINT " "
1330 INPUT "Do You Wish to Load KEYS from a FILE (Y OR N) ",A$
1340 A$=LEFT$(A$,1)
1350 IF A$ = "Y" OR A$ = "y" THEN GOSUB 6460 : GOTO 2410
1360 CLS
1370 FOR J%=10 TO 15
1380 IF CL$="Y" THEN COLOR J%,9,1
1390 PRINT "P C - C O D E 2 ......Textual SuperEncipherment......"
1400 NEXT J%
1410 IF CL$="Y" THEN COLOR 15,9,1
1420 PRINT "  "
1430 PRINT "  "
1440 PRINT " ": PRINT " "
1450 PRINT " ": PRINT " "
1460 PRINT " Enter the type of KEY desired"
1470 PRINT "    Numeric; Number Only key ";
1480 IF CL$="Y" THEN COLOR 13,0,0: PRINT "N" : COLOR 15,9,1
1490 IF CL$="N" THEN PRINT "N"
1500 PRINT "    Alphabetic; alphanumeric ";
1510 IF CL$="Y" THEN COLOR 13,0,0: PRINT "A" : COLOR 15,9,1
1520 IF CL$="N" THEN PRINT "A"
1530 INPUT " Enter N or A ", T$
1540 T$=LEFT$(T$,1)
1550 IF T$="a" THEN T$="A"
1560 IF T$="n" THEN T$="N"
1570 IF T$ <> "A" AND T$ <> "N" THEN 1530
1580 IF T$="N" THEN 1630
1590 IF T$="A" THEN 3650
1600 '
1610 '
1620 '
1630 CLS
1640 PRINT WZ$
1650 PRINT " "
1660 PRINT " "
1670 PRINT " There are two(2) levels of Security HIGH and LOW"
1680 INPUT " Enter H for HIGH or L for LOW ", A$
1690 A$=LEFT$(A$,1)
1700 IF A$ <> "H" AND A$ <> "h" AND A$ <> "L" AND A$ <> "l" THEN 1660
1710 IF A$ = "H" OR A$ = "h" THEN 1840
1720 '
1730 '   ----------- LOW level of SECURITY -------------
1740 '
1750 PRINT " LOW Level of Security Selected"
1760 PRINT " "
1770 PRINT " You must now enter SEVEN (7) KEY numbers as indicated:"
1780 PRINT " "
1790 GOSUB 3860
1800 GOTO 2410
1810 '
1820 '
1830 '
1840 PRINT " "
1850 PRINT " You have Selected HIGH security"
1860 PRINT " "
1870 PRINT " You must enter ";1+N%+M%;" key numbers between 1 and 2,147,483,646"
1880 INPUT " A(1) ? ", A#
1890 A#=FIX(A#)
1900 IF A# < 1 OR A# >= MD# THEN GOSUB 2260: GOTO 1880
1910 PRINT " "
1920 '
1930 '
1940 FOR J%=1 TO M%
1950 PRINT " B(";J%;") ";
1960 INPUT B#(J%)
1970 B#(J%)=FIX(B#(J%))
1980 IF B#(J%) < 1 OR B#(J%) >= MD# THEN GOSUB 2260: GOTO 1950
1990 NEXT J%
2000 CLS
2010 '
2020 '
2030 FOR J%=1 TO N%
2040 PRINT " C(";J%;") ";
2050 INPUT C#(J%)
2060 C#(J%)=FIX(C#(J%))
2070 IF C#(J%) < 1 OR C#(J%) >= MD# THEN GOSUB 2260: GOTO 2040
2080 IF J% = 18 THEN CLS
2090 IF J% = 36 THEN CLS
2100 NEXT J%
2110 '
2120 GOTO 2410
2130 '
2140 '
2150 '
2160 PRINT "[";X%;"] ";
2170 INPUT "Enter a NUMBER between 1 and 9,999,999 ", K2!
2180 IF K2! < 1 OR K2! > 9999999! THEN GOSUB 2260: GOTO 2160
2190 Z!=K2!
2200 GOSUB 2350
2210 S!=Z!
2220 RETURN
2230 '
2240 '
2250 '   ---------- ERROR Messages ----------
2260 BEEP
2270 IF CL$="Y" THEN COLOR 4+16,0,0
2280 PRINT " ERROR: Number RANGE must be 1 to 9,999,999"
2290 IF CL$="Y" THEN COLOR 15,1,9
2300 BEEP
2310 RETURN
2320 '
2330 '
2340 '   ------ scaling ---------
2350 Z! = Z! / 100!
2360 IF Z! > 1! THEN 2350
2370 RETURN
2380 '
2390 '
2400 '
2410 CLS
2420 PRINT WZ$
2430 PRINT " ": PRINT " ": PRINT " "
2440 PRINT "Possible Output files:"
2450 PRINT "  'A:FILE.EXT'  'B:FILE.EXT'  'LPT1:'  'CON:'  'CAS1:'"
2460 PRINT " "
2470 INPUT "Enter Output file name (Full name): ", U$
2480 IF U$ = "con:" THEN U$="CON:"
2490 IF U$ <> "CON:" THEN OPEN "O",2,U$
2500 PRINT " ": PRINT " "
2510 PRINT "Possible Input files:"
2520 PRINT "  'A:FILE.EXT'  'B:FILE.EXT'  'C:FILE.EXT'  'CON:'  'CAS1:'"
2530 PRINT " "
2540 INPUT "Enter Input FILE (full name): ", F1$
2550 IF F1$= "con:" THEN F1$="CON:"
2560 IF F1$ <> "CON:" THEN OPEN "I",1,F1$
2570 PRINT " "
2580 PRINT " "
2590 INPUT "Encode or Decode (E or D) ",EN$
2600 EN$=LEFT$(EN$,1)
2610 IF EN$ = "e" THEN EN$="E"
2620 IF EN$ = "d" THEN EN$="D"
2630 IF EN$ <> "D" AND EN$ <> "E" THEN 2590
2640 IF EN$="E" AND U$ <> "CON:" THEN PRINT #2, HDR$
2650 IF EN$ <> "D" THEN 2720
2660 IF F1$="CON:" THEN 2720
2670 LINE INPUT #1, HDR$
2680 IF LEFT$(HDR$,14) = WH$ THEN 2720
2690 PRINT "* * * File was never Encoded by "; WH$
2700 PRINT "      file header = ", HDR$
2710 END
2720 CLS: PRINT WZ$: PRINT " ": PRINT " "
2730 IF CL$="Y" THEN COLOR 4+16,0,0
2740 IF CL$="N" THEN COLOR 7+16,0
2750 PRINT " * * * R U N N I N G * * *"
2760 IF CL$="Y" THEN COLOR 15,9,1
2770 IF CL$="N" THEN COLOR 7,0
2780 PRINT " "
2790 IF F1$="CON:" THEN PRINT "--> Terminate Console Input with use '//END'"
2800 IF EN$="D" AND F1$ <> "CON:" THEN PRINT "File Header: "; HDR$
2810 PRINT " "
2820 IF F1$ = "CON:" THEN 2860
2830 IF EOF(1) <> 0 THEN 3390
2840 LINE INPUT #1, M$
2850 GOTO 2880
2860 LINE INPUT M$
2870 IF M$ = "//END" OR M$ = "//end" THEN 3390
2880 L%=LEN(M$)
2890 IF L% = 0 THEN 3190
2900 FOR JJ%=1 TO L%
2910 IX%(JJ%)=JJ%
2920 NEXT JJ%
2930 L9%=INT(L%/2)
2940 GOSUB 5710
2950 IF EN$="D" THEN GOSUB 5920
2960 FOR J%=1 TO L%
2970 H%=ASC( MID$(M$,J%,1))
2980 H% = H% - 32
2990 S#=A#
3000 L=M%
3010 GOSUB 3250
3020 A#=S#
3030 B%=O%
3040 L=N%
3050 S#=B#(B%)
3060 GOSUB 3250
3070 B#(B%)=S#
3080 B%=O%
3090 S#=C#(B%)
3100 L=L0%
3110 GOSUB 3250
3120 C#(B%)=S#
3130 IF EN$="E" THEN H% = H% + O%
3140 IF EN$="D" THEN H% = K0% + (H% - O%)
3150 H% = H% MOD L0%
3160 MID$(M$,J%,1)=CHR$(H% + 32)
3170 NEXT J%
3180 IF EN$="E" THEN GOSUB 5920
3190 IF U$ <> "CON:" THEN PRINT #2, M$
3200 IF U$ = "CON:" THEN PRINT M$ : PRINT " "
3210 GOTO 2820
3220 '
3230 '
3240 '   -------- RANDOM NUMBER GENERATOR (1) ------
3250 S#=S# * MU#
3260 S#=S# - (MD# * INT(S# / MD#) )
3270 O%=1 + FIX(L * (S# / MD#) )
3280 RETURN
3290 '
3300 '
3310 '   -------- RANDOM NUMBER GENERATOR (2) ------
3320 S!=(S! + 1.352968) ^ 1.256973
3330 S!=S! - FIX(S!)
3340 O%=1 + FIX(L * S!)
3350 RETURN
3360 '
3370 '
3380 '
3390 PRINT " "
3400 IF U$ <> "CON:" THEN CLOSE 2
3410 IF F1$ <> "CON:" THEN CLOSE 1
3420 IF U$="CON:" THEN D! = 5 : GOSUB 6830 ' TIME DELAY                 $
3430 '
3440 '
3450 ' ------- CLEAR MEMORY & STOP --------
3460 INPUT "Wish to Save newly Computed KEYS to a FILE (Y OR N) ", F$
3470 F$=LEFT$(F$,1)
3480 IF F$="Y" OR F$ = "y" THEN GOSUB 6660
3490 Z!=0: A#=0: M$=SPACE$(128): S! = 0 : S# = 0
3500 FOR J%=1 TO M%
3510 B#(J%)=0
3520 NEXT J%
3530 FOR J%=1 TO N%
3540 C#(J%)=0
3550 NEXT J%
3560 FOR J%=1 TO 256
3570 IX%(J%)=0
3580 NEXT J%
3590 COLOR 7,0
3600 CLS
3610 END
3620 '
3630 '   ----------- ALPHANUMERIC KEYS -----------
3640 '
3650 CLS
3660 PRINT WZ$
3670 PRINT " "
3680 PRINT " "
3690 PRINT " There are two(2) levels of Security HIGH and LOW"
3700 INPUT " Enter H for HIGH or L for LOW ", A$
3710 A$=LEFT$(A$,1)
3720 IF A$ <> "H" AND A$ <> "h" AND A$ <> "L" AND A$ <> "l" THEN 3680
3730 IF A$ = "H" OR A$ = "h" THEN 4940
3740 '
3750 '   ----------- LOW level of SECURITY -------------
3760 '
3770 PRINT " LOW Level of Security Selected"
3780 PRINT " "
3790 PRINT " You must now enter SEVEN (7) key Alphanumerics as indicated:"
3800 PRINT " "
3810 GOSUB 3860
3820 GOTO 2410
3830 '
3840 '
3850 '   ---------- KEY 1 ---------
3860 X%=1 : M% = 11 : N% = 47
3870 IF T$ = "A" THEN GOSUB 5070
3880 IF T$ = "N" THEN GOSUB 2160
3890 GOSUB 3320
3900 GOSUB 3320
3910 A#=FIX((1# - S!) * MD#)
3920 '
3930 '
3940 '   ---------- KEY 2 ----------
3950 X%=2
3960 IF T$ = "A" THEN GOSUB 5070
3970 IF T$ = "N" THEN GOSUB 2160
3980 GOSUB 3320
3990 L=4
4000 FOR J%=1 TO M%
4010 GOSUB 3320
4020 O2% = O%
4030 FOR K%=1 TO O2%
4040 GOSUB 3320
4050 NEXT K%
4060 GOSUB 3320
4070 B#(J%)=FIX((1# - S!) * MD#)
4080 NEXT J%
4090 '
4100 '
4110 '   ---------- KEY 3 -----------
4120 X%=3
4130 IF T$ = "A" THEN GOSUB 5070
4140 IF T$ = "N" THEN GOSUB 2160
4150 GOSUB 3320
4160 L=3
4170 FOR J%=1 TO N%
4180 GOSUB 3320
4190 O2% = O%
4200 FOR K%=1 TO O2%
4210 GOSUB 3320
4220 NEXT K%
4230 GOSUB 3320
4240 C#(J%)=FIX((1# - S!) * MD#)
4250 NEXT J%
4260 '
4270 '
4280 '   ---------- KEY 4 -------------
4290 X%=4
4300 IF T$ = "A" THEN GOSUB 5070
4310 IF T$ = "N" THEN GOSUB 2160
4320 GOSUB 3320
4330 L=INT(N%/2)
4340 GOSUB 3320
4350 K%=O% + 1
4360 L=N%
4370 FOR J%=1 TO K%
4380 GOSUB 3320
4390 L%=O%
4400 GOSUB 3320
4410 C#(L%)=FIX(S! * MD#)
4420 NEXT J%
4430 '
4440 '
4450 '   ----------- KEY 5 ---------------
4460 X%=5
4470 IF T$ = "A" THEN GOSUB 5070
4480 IF T$ = "N" THEN GOSUB 2160
4490 GOSUB 3320
4500 L=INT(M%/2)
4510 GOSUB 3320
4520 K%=O% + 1
4530 L=M%
4540 FOR J%=1 TO K%
4550 GOSUB 3320
4560 L%=O%
4570 GOSUB 3320
4580 B#(L%)=FIX(S! * MD#)
4590 NEXT J%
4600 '
4610 '
4620 '   ------------ KEY 6 ---------------
4630 X%=6
4640 IF T$ = "A" THEN GOSUB 5070
4650 IF T$ = "N" THEN GOSUB 2160
4660 GOSUB 3320
4670 L=M%
4680 FOR J%=1 TO M%
4690 GOSUB 3320
4700 D#=B#(O%)
4710 B#(O%)=B#(J%)
4720 B#(J%)=D#
4730 NEXT J%
4740 '
4750 '
4760 '   ------------- KEY 7 --------------
4770 X%=7
4780 IF T$ = "A" THEN GOSUB 5070
4790 IF T$ = "N" THEN GOSUB 2160
4800 GOSUB 3320
4810 L=N%
4820 FOR J%=1 TO N%
4830 GOSUB 3320
4840 D#=C#(O%)
4850 C#(O%)=C#(J%)
4860 C#(J%)=D#
4870 NEXT J%
4880 '
4890 RETURN
4900 '
4910 '   ---------- end of LOW security ------------
4920 '
4930 '
4940 CLS
4950 PRINT " You must enter 3 long PASSWORDS of alphanumeric data"
4960 X%=1
4970 GOSUB 5260
4980 X%=M%
4990 GOSUB 5260
5000 X%=N%
5010 GOSUB 5260
5020 '
5030 GOTO 2410
5040 '
5050 '
5060 '   --------- alphanumeric password to RND ------------
5070 PRINT "[";X%;"] Enter Password: ";
5080 LINE INPUT P$
5090 L%=LEN(P$)
5100 IF L% < 6 THEN PRINT "  *** password too short; not > 5": GOTO 5070
5110 IF X%=1 THEN K# = 1
5120 FOR J%=1 TO L%
5130 C%=ASC( MID$(P$,J%,1) )
5140 LL%=L%
5150 IF LL% > 7 THEN LL% = LL% MOD 7 : LL%=LL%+1
5160 K# = K# + (CONS!(LL%) * C%)
5170 NEXT J%
5180 Z! = K#
5190 GOSUB 2340
5200 K# = K# - (YY# * INT( K# / YY#))
5210 S! = Z!
5220 RETURN
5230 '
5240 '
5250 '   ------- alphanumeric to DECIMAL --------
5260 X2%=X% * 6
5270 X3% = X2% : X4% = 0
5280 IF X%=1 THEN PRINT " (A) Enter Password of at least (MIN) ";X2%;" Chars"
5290 IF X%=M% THEN PRINT " (B) Enter Password of at least (MIN) ";X2%;" Chars"
5300 IF X2% > 60 THEN X3% = 60: X4% = X2% - 60: CLS: PRINT WZ$: PRINT " "
5310 IF X%=N% THEN PRINT " (C) Enter Password of at least (MIN) ";X2%;" Chars"
5320 PRINT "  ": PRINT " "
5330 PRINT "         "; LEFT$(W2$,X3%)
5340 PRINT "         "; LEFT$(W1$,X3%)
5350 PRINT "         "; LEFT$(W3$,X3%)
5360 PRINT "Password:";
5370 LINE INPUT P$
5380 PRINT "  "
5390 IF X4% = 0 THEN 5470
5400 PRINT "  ": PRINT " "
5410 PRINT "         "; LEFT$(W4$,X4%)
5420 PRINT "         "; LEFT$(W1$,X4%)
5430 PRINT "         "; LEFT$(W3$,X4%)
5440 PRINT "Password:";
5450 LINE INPUT P2$
5460 P$ = P$ + P2$
5470 L%=LEN(P$)
5480 IF L% < X2% THEN PRINT "   *** Password TOO SHORT reenter ": GOTO 5260
5490 T%=INT(L%/X%)
5500 IF X%=1 THEN K#=1
5510 FOR K%=1 TO X%
5520 P2$=LEFT$(P$,T%)
5530 IF L% < 1 THEN 5650
5540 P$=MID$(P$,T%+1,L%)
5550 L%=L%-T%
5560 FOR J%=1 TO T%
5570 LL%=J%
5580 IF LL% > 7 THEN LL%= LL% MOD 7 : LL%=LL%+1
5590 K# =  K# + (CONS!(LL%) * C%)
5600 NEXT J%
5610 IF X%=1 THEN A#=K#
5620 IF X%=M% THEN B#(K%) = K#
5630 IF X%=N% THEN C#(K%) = K#
5640 K# = K# - (MD# * INT( K# / MD# ))
5650 NEXT K%
5660 RETURN
5670 '
5680 '
5690 '   ------ TRANSPOSITION -------
5700 '
5710 FOR JJ%=1 TO L9%
5720 L=M%
5730 S#=A#
5740 GOSUB 3250
5750 A#=S#
5760 B%=O%
5770 L=N%
5780 S#=B#(B%)
5790 GOSUB 3250
5800 B#(B%)=S#
5810 B%=O%
5820 S#=C#(B%)
5830 L=L%
5840 GOSUB 3250
5850 C#(B%)=S#
5860 SWAP IX%(JJ%),IX%(O%)
5870 NEXT JJ%
5880 RETURN
5890 '
5900 '
5910 '
5920 FOR JJ%=1 TO L9%
5930 G1%=IX%(JJ%)
5940 G2%=IX%(JJ%+L9%)
5950 G1$=MID$(M$,G1%,1)
5960 G2$=MID$(M$,G2%,1)
5970 SWAP G1$,G2$
5980 MID$(M$,G1%,1)=G1$
5990 MID$(M$,G2%,1)=G2$
6000 NEXT JJ%
6010 RETURN
6020 '
6030 '
6040 '
6050 ' ------ Help / Instructions -----
6060 OPEN "I",3,"PC-CODE2.DOC"
6070 CLS
6080 JJ%=1
6090 PRINT WZ$: PRINT " ": PRINT " "
6100 IF EOF(3) <> 0 THEN 6190
6110 LINE INPUT #3, M$
6120 PRINT M$
6130 JJ%=JJ%+1
6140 IF JJ% < 19 THEN 6100
6150 PRINT "-----------------------"
6160 INPUT "Wish More Documentation (Yes or No) (ENTER=Yes) ", Z$
6170 Z$=LEFT$(Z$,1)
6180 IF Z$ <> "N" AND Z$ <> "n" THEN 6070
6190 CLOSE 3
6200 RETURN
6210 '
6220 '
6230 '
6240 CLS
6250 PRINT WZ$: PRINT " ": PRINT " ":PRINT " "
6260 INPUT "Do you have COLOR installed on your PC (Yes or No) ",Z$
6270 Z$=LEFT$(Z$,1)
6280 IF Z$="y" THEN Z$="Y"
6290 IF Z$="n" THEN Z$="N"
6300 IF Z$ <> "Y" AND Z$ <> "N" THEN 6260
6310 CL$=Z$
6320 IF CL$="N" THEN 6350
6330 SCREEN 0,1
6340 COLOR 15,9,1
6350 PRINT " ": PRINT " "
6360 INPUT "Do you wish some instructions/Help (Yes or No) ",Z$
6370 Z$=LEFT$(Z$,1)
6380 IF Z$="y" THEN Z$="Y"
6390 IF Z$="n" THEN Z$="N"
6400 IF Z$ <> "Y" AND Z$ <> "N" THEN 6360
6410 IF Z$="Y" THEN GOSUB 6050
6420 RETURN
6430 '
6440 '
6450 '
6460 PRINT " " : T% = N% + M% + 1
6470 PRINT "The key FILE must have "; T%; " Numbers separated by <CR>"
6480 INPUT "Enter the Fully Qualified Input Key FILE name ", F$
6490 OPEN "I", 5, F$
6500 INPUT #5, A#
6510 A# = ABS( FIX( A# ) )
6520 IF EOF(5) <> 0 THEN PRINT "*** ERROR *** Reading Key File": END
6530 FOR J%=1 TO M% : INPUT #5, B#(J%)
6540 B#(J%) = ABS( FIX( B#(J%) ) ) : NEXT J%
6550 IF EOF(5) <> 0 THEN PRINT "*** ERROR *** Reading Key File": END
6560 FOR J%=1 TO N% : INPUT #5, C#(J%)
6570 C#(J%) = ABS( FIX( C#(J%) ) ) : NEXT J%
6580 IF EOF(5) <> 0 THEN PRINT "*** ERROR *** Reading Key File": END
6590 PRINT "*** Loaded "; T% ;" Keys from ", F$, " Successfully"
6600 PRINT " ": D! = 4 : GOSUB 6830  ' TIME DELAY
6610 CLOSE 5
6620 RETURN
6630 '
6640 '
6650 '
6660 INPUT "Enter the Fully Qualified Output Key File Name ", F$
6670 OPEN "O", 5, F$
6680 PRINT #5, A#
6690 FOR J%=1 TO M% : PRINT #5, B#(J%)
6700 NEXT J%
6710 FOR J%=1 TO N% : PRINT #5, C#(J%)
6720 NEXT J%
6730 PRINT #5, "* Keys computed on " + DATE$ + " " + TIME$ + " " + WH$
6740 T% = M% + N% + 1
6750 PRINT "*** Saved "; T% ;" Keys to ", F$, " Successfully"
6760 PRINT " ": D! = 4 : GOSUB 6830  ' TIME DELAY
6770 CLOSE 5
6780 RETURN
6790 '
6800 '
6810 '
6820 ' ------- Delay function -------
6830 GOSUB 6930
6840 D1! = TI!
6850 GOSUB 6930  '  Loop Here
6860 D2! = ABS( TI! - D1! )
6870 IF D2! < D! THEN 6850
6880 RETURN
6890 '
6900 '
6910 '
6920 ' ------- TIMER function --------
6930 TI! = VAL(LEFT$(TIME$,2)) * 3600
6940 TI! = TI! + (VAL( MID$(TIME$,4,2)) * 60)
6950 TI! = TI! + VAL(RIGHT$(TIME$,2))
6960 RETURN   ' TI! now contains seconds like TIMER
6970 '
6980 '
6990 '
7000 END

6950 TI! = TI! + VAL(RIGHT$(TIME$,2))
6960 RETURN   ' TI! now contains seconds like TIMER
6970 '
6980 '
699