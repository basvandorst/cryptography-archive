1000 '
1010 '
1020 '         <<<<<  P C - C O D E 1  >>>>>
1030 '
1040 '
1050 '     * * * * * * * * * * * * * * * * * * * *
1060 '     *                                     *
1070 '     *  COPYRIGHT in Public Domain 1984    *
1080 '     *           by Richard N. Colvard     *
1090 '     *                                     *
1100 '     *  WARNING: This Program must be      *
1110 '     *           Compiled with IBM         *
1120 '     *           BASCOM/T/O                *
1130 '     *           Donot use BASICA          *
1140 '     *                                     *
1150 '     *  Highly recommend 8087 Link Libs    *
1160 '     *                                     *
1170 '     * * * * * * * * * * * * * * * * * * * *
1180 '
1190 '
1200 DIM B#(11), C#(47), CONS!(7), IX%(128)
1210 DIM ZI$(4), ZO$(4)   ' Dummy dimensions for field stmst ND error
1220 CONS!(1)=8: CONS!(2)=131072! : CONS!(3)=8192: CONS!(4)=128
1230 CONS!(5)=2: CONS!(6)=32 : CONS!(7)=2048
1240 M%=7: N%=17: YY# = 999991! : MU# = 16807 : MD# = 2147483647#
1250 W1$="12345678901234567890123456789012345678901234567890123456789012345678"
1260 W2$="         1         2         3         4         5         6"
1270 W3$=".........+.........+.........+.........+.........+.........+........"
1280 W4$="         7         8         9        10        11        12"
1290 WZ$="P C   C O M P U T E R   S E C U R I T Y    V1.37   PC-CODE1"
1300 FOR J%=1 TO 128
1310 IX%(J%)=129 - J%
1320 NEXT J%
1330 CLS
1340 PRINT WZ$: PRINT " ": PRINT " ":PRINT " "
1350 INPUT "Do you have COLOR installed on your PC (YES or NO) ",Z$
1360 Z$=LEFT$(Z$,1)
1370 IF Z$="y" THEN Z$="Y"
1380 IF Z$="n" THEN Z$="N"
1390 IF Z$ <> "Y" AND Z$ <> "N" THEN 1350
1400 CL$=Z$
1410 IF CL$="N" THEN 1440
1420 SCREEN 0,1
1430 COLOR 15,9,1
1440 PRINT " ": PRINT " "
1450 INPUT "Do You wish some Instructions/Help (Yes or No) ",Z$
1460 Z$=LEFT$(Z$,1)
1470 IF Z$="y" THEN Z$="Y"
1480 IF Z$="n" THEN Z$="N"
1490 IF Z$ <> "Y" AND Z$ <> "N" THEN 1450
1500 IF Z$="Y" THEN GOSUB 6270
1510 PRINT " " : PRINT " "
1520 INPUT "Do You Wish to Load KEYS from a FILE (Y or N) ",A$
1530 A$=LEFT$(A$,1)
1540 IF A$ = "Y" OR A$ = "y" THEN GOSUB 6470 : GOTO 2600
1550 CLS
1560 FOR J%=10 TO 15
1570 IF CL$="Y" THEN COLOR J%,9,1
1580 PRINT "P C - C O D E 1 ......Binary SuperEncipherment......"
1590 NEXT J%
1600 IF CL$="Y" THEN COLOR 15,9,1
1610 PRINT "  "
1620 PRINT "  "
1630 PRINT " ": PRINT " "
1640 PRINT " ": PRINT " "
1650 PRINT " Enter the type of KEY desired"
1660 PRINT "    Numeric; Number Only key ";
1670 IF CL$="Y" THEN COLOR 13,0,0: PRINT "N" : COLOR 15,9,1
1680 IF CL$="N" THEN PRINT "N"
1690 PRINT "    Alphabetic; alphanumeric ";
1700 IF CL$="Y" THEN COLOR 13,0,0: PRINT "A" : COLOR 15,9,1
1710 IF CL$="N" THEN PRINT "A"
1720 INPUT " Enter N or A ", T$
1730 T$=LEFT$(T$,1)
1740 IF T$="a" THEN T$="A"
1750 IF T$="n" THEN T$="N"
1760 IF T$ <> "A" AND T$ <> "N" THEN 1720
1770 IF T$="N" THEN 1820
1780 IF T$="A" THEN 3740
1790 '
1800 '
1810 '
1820 CLS
1830 PRINT WZ$
1840 PRINT " "
1850 PRINT " "
1860 PRINT " There are two(2) levels of Security HIGH and LOW"
1870 INPUT " Enter H for HIGH or L for LOW ", A$
1880 A$=LEFT$(A$,1)
1890 IF A$ <> "H" AND A$ <> "h" AND A$ <> "L" AND A$ <> "l" THEN 1850
1900 IF A$ = "H" OR A$ = "h" THEN 2030
1910 '
1920 '   ----------- LOW level of SECURITY -------------
1930 '
1940 PRINT " LOW Level of Security Selected"
1950 PRINT " "
1960 PRINT " You must now enter SEVEN (7) KEY numbers as indicated:"
1970 PRINT " "
1980 GOSUB 3950
1990 GOTO 2600
2000 '
2010 '
2020 '
2030 PRINT " "
2040 PRINT " You have Selected HIGH security"
2050 PRINT " "
2060 PRINT " You must enter ";1+N%+M%;" key numbers between 1 and 2,147,483,646"
2070 INPUT " A( 1 ) ? ", A#
2080 A#=FIX(A#)
2090 IF A# < 1 OR A# >= MD# THEN GOSUB 2450: GOTO 2070
2100 PRINT " "
2110 '
2120 '
2130 FOR J%=1 TO M%
2140 PRINT " B(";J%;") ";
2150 INPUT B#(J%)
2160 B#(J%)=FIX(B#(J%))
2170 IF B#(J%) < 1 OR B#(J%) >= MD# THEN GOSUB 2450: GOTO 2140
2180 NEXT J%
2190 CLS
2200 '
2210 '
2220 FOR J%=1 TO N%
2230 PRINT " C(";J%;") ";
2240 INPUT C#(J%)
2250 C#(J%)=FIX(C#(J%))
2260 IF C#(J%) < 1 OR C#(J%) >= MD# THEN GOSUB 2450: GOTO 2230
2270 IF J% = 18 THEN CLS
2280 IF J% = 36 THEN CLS
2290 NEXT J%
2300 '
2310 GOTO 2600
2320 '
2330 '
2340 '
2350 PRINT "[";X%;"] ";
2360 INPUT "Enter a NUMBER between 1 and 9,999,999 ", K2!
2370 IF K2! < 1 OR K2! > 9999999! THEN GOSUB 2450: GOTO 2350
2380 Z!=K2!
2390 GOSUB 2540
2400 S!=Z!
2410 RETURN
2420 '
2430 '
2440 '   ---------- ERROR Messages ----------
2450 BEEP
2460 IF CL$="Y" THEN COLOR 4+16,0,0
2470 PRINT " ERROR: Number RANGE must be 1 to 9,999,999"
2480 IF CL$="Y" THEN COLOR 15,1,9
2490 BEEP
2500 RETURN
2510 '
2520 '
2530 '   ------ scaling ---------
2540 Z! = Z! / 100!
2550 IF Z! > 1! THEN 2540
2560 RETURN
2570 '
2580 '
2590 '
2600 CLS
2610 PRINT " "
2620 PRINT WZ$
2630 PRINT " "
2640 PRINT " ": PRINT " "
2650 PRINT " Input and Output File may be the same file"
2660 PRINT " Only Valid files; no use of 'CON:' or 'LPT1:'"
2670 PRINT " ":PRINT " "
2680 INPUT "Enter Output file name (Full name): ", U$
2690 OPEN "R",2,U$,512
2700 INPUT "Enter Input FILE (full name): ", F1$
2710 OPEN "R",1,F1$,512
2720 FIELD #1,128 AS ZI$(1),128 AS ZI$(2),128 AS ZI$(3),128 AS ZI$(4)
2730 FIELD #2,128 AS ZO$(1),128 AS ZO$(2),128 AS ZO$(3),128 AS ZO$(4)
2740 L!=LOF(1) : SIZE%=L!/128
2750 IF (SIZE% * 128!) <> L! THEN SIZE% = SIZE% + 1
2760 SIZ2% = L! / 512
2770 IF (SIZ2% * 512) <> L! THEN SIZ2% = SIZ2% + 1
2780 PRINT " "
2790 INPUT "Encode or Decode (E or D) ",EN$
2800 EN$=LEFT$(EN$,1)
2810 IF EN$ = "e" THEN EN$="E"
2820 IF EN$ = "d" THEN EN$="D"
2830 IF EN$ <> "D" AND EN$ <> "E" THEN 2790
2840 CLS: PRINT WZ$: PRINT " ": PRINT " "
2850 END$=STRING$(12,0)
2860 IF CL$="Y" THEN COLOR 4+16,0,0
2870 IF CL$="N" THEN COLOR 7+16,0
2880 PRINT " * * * R U N N I N G * * *"
2890 IF CL$="Y" THEN COLOR 15,9,1
2900 IF CL$="N" THEN COLOR 7,0
2910 PRINT " "
2920 LAST%=0
2930 FOR Z9% = 1 TO SIZ2%
2940 GET #1, Z9%
2950 GOSUB 5800
2960 FOR JK% = 1 TO 4
2970 M2$=STRING$(128,0)
2980 IF LAST% >= SIZE% THEN LSET ZO$(JK%)=M2$ : GOTO 3260
2990 M$=ZI$(JK%)
3000 IF EN$="D" AND LEFT$(M$,12)=END$ THEN 3260
3010 L%=LEN(M$)
3020 IF L% = 0 THEN 3240
3030 IF EN$="D" THEN GOSUB 6010
3040 FOR J%=1 TO L%
3050 H%=ASC( MID$(M$,J%,1))
3060 S#=A#
3070 L=M%
3080 GOSUB 3340
3090 A#=S#
3100 B%=O%
3110 L=N%
3120 S#=B#(B%)
3130 GOSUB 3340
3140 B#(B%)=S#
3150 B%=O%
3160 S#=C#(B%)
3170 L=256
3180 GOSUB 3340
3190 C#(B%)=S#
3200 H%=H% XOR O%
3210 MID$(M2$,J%,1)=CHR$(H% AND 255)
3220 NEXT J%
3230 IF EN$="E" THEN GOSUB 6140
3240 LSET ZO$(JK%) = M2$
3250 LAST% = LAST% + 1
3260 NEXT JK%
3270 PUT #2, Z9%
3280 NEXT Z9%
3290 '
3300 GOTO 3490
3310 '
3320 '
3330 '   -------- RANDOM NUMBER GENERATOR (1) ------
3340 S# = S# * MU#
3350 S# = S# - (MD# * INT( S# / MD# )  )
3360 O%=1 + INT(L * (S# / MD#) )
3370 RETURN
3380 '
3390 '
3400 '   -------- RANDOM NUMBER GENERATOR (2) ------
3410 S!=(S! + 1.352968) ^ 1.256973
3420 S!=S! - FIX(S!)
3430 O%=1 + INT(L * S!)
3440 RETURN
3450 '
3460 '
3470 '
3480 ' ------- CLEAR STORAGE & PREPARE TO STOP ---------
3490 LSET ZO$(1)=SPACE$(128): LSET ZI$(1)=SPACE$(128)
3500 LSET ZO$(2)=SPACE$(128): LSET ZI$(2)=SPACE$(128)
3510 LSET ZO$(3)=SPACE$(128): LSET ZI$(3)=SPACE$(128)
3520 LSET ZO$(4)=SPACE$(128): LSET ZI$(4)=SPACE$(128)
3530 CLOSE 2
3540 CLOSE 1
3550 INPUT "Wish to Save newly Computed Keys to a File (Y or N) ", F$
3560 F$=LEFT$(F$,1)
3570 IF F$="Y" OR F$ = "y" THEN GOSUB 6670
3580 Z!=0: A#=0: M2$=SPACE$(128): M$=M2$ : S! = 0: S# = 0
3590 FOR J%=1 TO M%
3600 B#(J%)=0
3610 NEXT J%
3620 FOR J%=1 TO N%
3630 C#(J%)=0
3640 NEXT J%
3650 FOR J%=1 TO 128
3660 IX%(J%)=0
3670 NEXT J%
3680 IF CL$="Y" THEN COLOR 7,0,0
3690 CLS
3700 END              '   S T O P
3710 '
3720 '   ----------- ALPHANUMERIC KEYS -----------
3730 '
3740 CLS
3750 PRINT WZ$
3760 PRINT " "
3770 PRINT " "
3780 PRINT " There are two(2) levels of Security HIGH and LOW"
3790 INPUT " Enter H for HIGH or L for LOW ", A$
3800 A$=LEFT$(A$,1)
3810 IF A$ <> "H" AND A$ <> "h" AND A$ <> "L" AND A$ <> "l" THEN 3770
3820 IF A$ = "H" OR A$ = "h" THEN 5030
3830 '
3840 '   ----------- LOW level of SECURITY -------------
3850 '
3860 PRINT " LOW Level of Security Selected"
3870 PRINT " "
3880 PRINT " You must now enter SEVEN (7) key Alphanumerics as indicated:"
3890 PRINT " "
3900 GOSUB 3950
3910 GOTO 2600
3920 '
3930 '
3940 '   ---------- KEY 1 ---------
3950 X%=1 : M% = 11 : N% = 47
3960 IF T$ = "A" THEN GOSUB 5160
3970 IF T$ = "N" THEN GOSUB 2350
3980 GOSUB 3410
3990 GOSUB 3410
4000 A#=FIX((1# - S!) * MD#)
4010 '
4020 '
4030 '   ---------- KEY 2 ----------
4040 X%=2
4050 IF T$ = "A" THEN GOSUB 5160
4060 IF T$ = "N" THEN GOSUB 2350
4070 GOSUB 3410
4080 L=4
4090 FOR J%=1 TO M%
4100 GOSUB 3410
4110 O2% = O%
4120 FOR K%=1 TO O2%
4130 GOSUB 3410
4140 NEXT K%
4150 GOSUB 3410
4160 B#(J%)=FIX((1# - S!) * MD#)
4170 NEXT J%
4180 '
4190 '
4200 '   ---------- KEY 3 -----------
4210 X%=3
4220 IF T$ = "A" THEN GOSUB 5160
4230 IF T$ = "N" THEN GOSUB 2350
4240 GOSUB 3410
4250 L=3
4260 FOR J%=1 TO N%
4270 GOSUB 3410
4280 O2% = O%
4290 FOR K%=1 TO O2%
4300 GOSUB 3410
4310 NEXT K%
4320 GOSUB 3410
4330 C#(J%)=FIX((1# - S!) * MD#)
4340 NEXT J%
4350 '
4360 '
4370 '   ---------- KEY 4 -------------
4380 X%=4
4390 IF T$ = "A" THEN GOSUB 5160
4400 IF T$ = "N" THEN GOSUB 2350
4410 GOSUB 3410
4420 L=INT(N%/2)
4430 GOSUB 3410
4440 K%=O% + 1
4450 L=N%
4460 FOR J%=1 TO K%
4470 GOSUB 3410
4480 L%=O%
4490 GOSUB 3410
4500 C#(L%)=FIX(S! * MD#)
4510 NEXT J%
4520 '
4530 '
4540 '   ----------- KEY 5 ---------------
4550 X%=5
4560 IF T$ = "A" THEN GOSUB 5160
4570 IF T$ = "N" THEN GOSUB 2350
4580 GOSUB 3410
4590 L=INT(M%/2)
4600 GOSUB 3410
4610 K%=O% + 1
4620 L=M%
4630 FOR J%=1 TO K%
4640 GOSUB 3410
4650 L%=O%
4660 GOSUB 3410
4670 B#(L%)=FIX(S! * MD#)
4680 NEXT J%
4690 '
4700 '
4710 '   ------------ KEY 6 ---------------
4720 X%=6
4730 IF T$ = "A" THEN GOSUB 5160
4740 IF T$ = "N" THEN GOSUB 2350
4750 GOSUB 3410
4760 L=M%
4770 FOR J%=1 TO M%
4780 GOSUB 3410
4790 D#=B#(O%)
4800 B#(O%)=B#(J%)
4810 B#(J%)=D#
4820 NEXT J%
4830 '
4840 '
4850 '   ------------- KEY 7 --------------
4860 X%=7
4870 IF T$ = "A" THEN GOSUB 5160
4880 IF T$ = "N" THEN GOSUB 2350
4890 GOSUB 3410
4900 L=N%
4910 FOR J%=1 TO N%
4920 GOSUB 3410
4930 D#=C#(O%)
4940 C#(O%)=C#(J%)
4950 C#(J%)=D#
4960 NEXT J%
4970 '
4980 RETURN
4990 '
5000 '   ---------- end of LOW security ------------
5010 '
5020 '
5030 CLS
5040 PRINT " You must enter 3 long PASSWORDS of alphanumeric data"
5050 X%=1
5060 GOSUB 5350
5070 X%=M%
5080 GOSUB 5350
5090 X%=N%
5100 GOSUB 5350
5110 '
5120 GOTO 2600
5130 '
5140 '
5150 '   --------- alphanumeric password to RND ------------
5160 PRINT "[";X%;"] Enter Password: ";
5170 LINE INPUT P$
5180 L%=LEN(P$)
5190 IF L% < 6 THEN PRINT "  *** password too short; not > 5": GOTO 5160
5200 IF X% = 1 THEN K# = 1
5210 FOR J%=1 TO L%
5220 C%=ASC( MID$(P$,J%,1) )
5230 LL%=L%
5240 IF LL% > 7 THEN LL% = LL% MOD 7 : LL%=LL%+1
5250 K# = K# + (CONS!(LL%) * C%)
5260 NEXT J%
5270 Z! = K#
5280 GOSUB 2530
5290 K# = K# - ( YY# * INT( K# / YY#))
5300 S! = Z!
5310 RETURN
5320 '
5330 '
5340 '   ------- alphanumeric to DECIMAL --------
5350 X2%=X% * 6
5360 X3%=X2% : X4%=0
5370 IF X%=1 THEN PRINT " (A) Enter Password of at least (MIN) ";X2%;" Chars"
5380 IF X%=M% THEN PRINT " (B) Enter Password of at least (MIN) ";X2%;" Chars"
5390 IF X2% > 60 THEN X3%=60: X4%=X2% - 60: CLS : PRINT WZ$: PRINT " "
5400 IF X%=N% THEN PRINT " (C) Enter Password of at least (MIN) ";X2%;" Chars"
5410 PRINT "  "
5420 PRINT "         "; LEFT$(W2$,X3%)
5430 PRINT "         "; LEFT$(W1$,X3%)
5440 PRINT "         "; LEFT$(W3$,X3%)
5450 PRINT "Password:";
5460 LINE INPUT P$
5470 PRINT "  "
5480 IF X4% = 0 THEN 5560
5490 PRINT "  "
5500 PRINT "         "; LEFT$(W4$,X4%)
5510 PRINT "         "; LEFT$(W1$,X4%)
5520 PRINT "         "; LEFT$(W3$,X4%)
5530 PRINT "Password:";
5540 LINE INPUT P2$
5550 P$ = P$ + P2$
5560 L%=LEN(P$)
5570 IF L% < X2% THEN PRINT "   *** Password TOO SHORT reenter ": GOTO 5350
5580 T%=INT(L%/X%)
5590 IF X%=1 THEN K#=1
5600 FOR K%=1 TO X%
5610 P2$=LEFT$(P$,T%)
5620 IF L% < 1 THEN 5740
5630 P$=MID$(P$,T%+1,L%)
5640 L%=L%-T%
5650 FOR J%=1 TO T%
5660 LL%=J%
5670 IF LL% > 7 THEN LL% = LL% MOD 7 : LL%=LL%+1
5680 K# = K# + (CONS!(LL%) * C%)
5690 NEXT J%
5700 IF X%=1 THEN A#=K#
5710 IF X%=M% THEN B#(K%) = K#
5720 IF X%=N% THEN C#(K%) = K#
5730 K# = K# - (MD# * INT( K# / MD# ))
5740 NEXT K%
5750 RETURN
5760 '
5770 '
5780 '   ------ TRANSPOSITION -------
5790 '
5800 FOR JJ%=1 TO 64
5810 L=M%
5820 S#=A#
5830 GOSUB 3340
5840 A#=S#
5850 B%=O%
5860 L=N%
5870 S#=B#(B%)
5880 GOSUB 3340
5890 B#(B%)=S#
5900 B%=O%
5910 S#=C#(B%)
5920 L=128
5930 GOSUB 3340
5940 C#(B%)=S#
5950 SWAP IX%(JJ%),IX%(O%)
5960 NEXT JJ%
5970 RETURN
5980 '
5990 '
6000 '
6010 FOR JJ%=1 TO 64
6020 G1%=IX%(JJ%)
6030 G2%=IX%(JJ%+64)
6040 G1$=MID$(M$,G1%,1)
6050 G2$=MID$(M$,G2%,1)
6060 SWAP G1$,G2$
6070 MID$(M$,G1%,1)=G1$
6080 MID$(M$,G2%,1)=G2$
6090 NEXT JJ%
6100 RETURN
6110 '
6120 '
6130 '
6140 FOR JJ%=1 TO 64
6150 G1%=IX%(JJ%)
6160 G2%=IX%(JJ%+64)
6170 G1$=MID$(M2$,G1%,1)
6180 G2$=MID$(M2$,G2%,1)
6190 SWAP G1$,G2$
6200 MID$(M2$,G1%,1)=G1$
6210 MID$(M2$,G2%,1)=G2$
6220 NEXT JJ%
6230 RETURN
6240 '
6250 '
6260 '
6270 ' ------ Help / Instructions -----
6280 OPEN "I",#3,"PC-CODE1.DOC"
6290 CLS
6300 JJ%=1
6310 PRINT WZ$: PRINT " ": PRINT " "
6320 IF EOF(3) <> 0 THEN 6410
6330 LINE INPUT #3, M$
6340 PRINT M$
6350 JJ%=JJ%+1
6360 IF JJ% < 18 THEN 6320
6370 PRINT "-----------------------": PRINT " "
6380 INPUT "Wish More Documentation (Yes or No) (ENTER=Yes) ", Z$
6390 Z$=LEFT$(Z$,1)
6400 IF Z$ <> "N" AND Z$ <> "n" THEN 6290
6410 PRINT " "
6420 CLOSE 3
6430 RETURN
6440 '
6450 '
6460 '
6470 PRINT " " : T% = N% + M% + 1
6480 PRINT "The Key file must have "; T%; " numbers separated by <CR>"
6490 INPUT "Enter the fully qualified Input Key File Name ", F$
6500 OPEN "I", 5, F$
6510 INPUT #5, A#
6520 A# = ABS( FIX( A# ) )
6530 IF EOF(5) <> 0 THEN PRINT "*** ERROR *** Reading key file": END
6540 FOR J%=1 TO M% : INPUT #5, B#(J%)
6550 B#(J%) = ABS( FIX( B#(J%) ) ) : NEXT J%
6560 IF EOF(5) <> 0 THEN PRINT "*** ERROR *** Reading key file": END
6570 FOR J%=1 TO N% : INPUT #5, C#(J%)
6580 C#(J%) = ABS( FIX( C#(J%) ) ) : NEXT J%
6590 IF EOF(5) <> 0 THEN PRINT "*** ERROR *** Reading key file": END
6600 PRINT "*** Loaded "; T% ;" Keys from ", F$, " Successfully"
6610 PRINT " ": D! = 4 : GOSUB 6840  ' Delay 4 seconds
6620 CLOSE 5
6630 RETURN
6640 '
6650 '
6660 '
6670 INPUT "Enter the fully qualified Output Key File Name ", F$
6680 OPEN "O", 5, F$
6690 PRINT #5, A#
6700 FOR J%=1 TO M% : PRINT #5, B#(J%)
6710 NEXT J%
6720 FOR J%=1 TO N% : PRINT #5, C#(J%)
6730 NEXT J%
6740 PRINT #5, "* Keys Computed on " + DATE$ + " " + TIME$
6750 T% = M% + N% + 1
6760 PRINT "*** Saved "; T% ;" Keys to ", F$, " Successfully"
6770 PRINT " ": D! = 4 : GOSUB 6840  '  Delay 4 seconds
6780 CLOSE 5
6790 RETURN
6800 '
6810 '
6820 '
6830 ' -------- Delay Function -------
6840 GOSUB 6940
6850 D1! = TI!
6860 GOSUB 6940    ' Loop Here
6870 D2! = ABS( TI! - D1! )
6880 IF D2! < D! THEN 6860
6890 RETURN
6900 '
6910 '
6920 '
6930 ' ------ TIMER function ------
6940 TI! = VAL(LEFT$(TIME$,2)) * 3600
6950 TI! = TI! + (VAL(MID$(TIME$,4,2)) * 60)
6960 TI! = TI! + VAL(RIGHT$(TIME$,2))
6970 RETURN    ' TI! now contains seconds like TIMER
6980 '
6990 '
7000 '
7010 END
610 PRINT " ": D! = 4 : GOSUB 6840  ' Delay 4 secon